{
  "language": "Solidity",
  "settings": {
    "libraries": {},
    "metadata": {
      "useLiteralContent": true
    },
    "optimizer": {
      "enabled": true,
      "runs": 1000
    },
    "remappings": [],
    "outputSelection": {
      "*": {
        "*": [
          "*"
        ],
        "": [
          "*"
        ]
      }
    }
  },
  "sources": {
    "TBillPolicy.sol": {
      "content": "pragma solidity 0.7.6;\r\n\r\n/**\r\n * @title SafeMath\r\n * @dev Math operations with safety checks that revert on error\r\n */\r\nlibrary SafeMath {\r\n    /**\r\n     * @dev Multiplies two numbers, reverts on overflow.\r\n     */\r\n    function mul(uint256 a, uint256 b) internal pure returns (uint256) {\r\n        // Gas optimization: this is cheaper than requiring 'a' not being zero, but the\r\n        // benefit is lost if 'b' is also tested.\r\n        // See: https://github.com/OpenZeppelin/openzeppelin-solidity/pull/522\r\n        if (a == 0) {\r\n            return 0;\r\n        }\r\n\r\n        uint256 c = a * b;\r\n        require(c / a == b);\r\n\r\n        return c;\r\n    }\r\n\r\n    /**\r\n     * @dev Integer division of two numbers truncating the quotient, reverts on division by zero.\r\n     */\r\n    function div(uint256 a, uint256 b) internal pure returns (uint256) {\r\n        require(b > 0); // Solidity only automatically asserts when dividing by 0\r\n        uint256 c = a / b;\r\n        // assert(a == b * c + a % b); // There is no case in which this doesn't hold\r\n\r\n        return c;\r\n    }\r\n\r\n    /**\r\n     * @dev Subtracts two numbers, reverts on overflow (i.e. if subtrahend is greater than minuend).\r\n     */\r\n    function sub(uint256 a, uint256 b) internal pure returns (uint256) {\r\n        require(b <= a);\r\n        uint256 c = a - b;\r\n\r\n        return c;\r\n    }\r\n\r\n    /**\r\n     * @dev Adds two numbers, reverts on overflow.\r\n     */\r\n    function add(uint256 a, uint256 b) internal pure returns (uint256) {\r\n        uint256 c = a + b;\r\n        require(c >= a);\r\n\r\n        return c;\r\n    }\r\n\r\n    /**\r\n     * @dev Divides two numbers and returns the remainder (unsigned integer modulo),\r\n     * reverts when dividing by zero.\r\n     */\r\n    function mod(uint256 a, uint256 b) internal pure returns (uint256) {\r\n        require(b != 0);\r\n        return a % b;\r\n    }\r\n}\r\n\r\n\r\n/**\r\n * @title Initializable\r\n *\r\n * @dev Helper contract to support initializer functions. To use it, replace\r\n * the constructor with a function that has the `initializer` modifier.\r\n * WARNING: Unlike constructors, initializer functions must be manually\r\n * invoked. This applies both to deploying an Initializable contract, as well\r\n * as extending an Initializable contract via inheritance.\r\n * WARNING: When used with inheritance, manual care must be taken to not invoke\r\n * a parent initializer twice, or ensure that all initializers are idempotent,\r\n * because this is not dealt with automatically as with constructors.\r\n */\r\ncontract Initializable {\r\n    /**\r\n     * @dev Indicates that the contract has been initialized.\r\n     */\r\n    bool private initialized;\r\n\r\n    /**\r\n     * @dev Indicates that the contract is in the process of being initialized.\r\n     */\r\n    bool private initializing;\r\n\r\n    /**\r\n     * @dev Modifier to use in the initializer function of a contract.\r\n     */\r\n    modifier initializer() {\r\n        require(\r\n            initializing || isConstructor() || !initialized,\r\n            \"Contract instance has already been initialized\"\r\n        );\r\n\r\n        bool wasInitializing = initializing;\r\n        initializing = true;\r\n        initialized = true;\r\n\r\n        _;\r\n\r\n        initializing = wasInitializing;\r\n    }\r\n\r\n    /// @dev Returns true if and only if the function is running in the constructor\r\n    function isConstructor() private view returns (bool) {\r\n        // extcodesize checks the size of the code stored in an address, and\r\n        // address returns the current address. Since the code is still not\r\n        // deployed when running a constructor, any checks on its code size will\r\n        // yield zero, making it an effective way to detect if a contract is\r\n        // under construction or not.\r\n\r\n        // MINOR CHANGE HERE:\r\n\r\n        // previous code\r\n        // uint256 cs;\r\n        // assembly { cs := extcodesize(address) }\r\n        // return cs == 0;\r\n\r\n        // current code\r\n        address _self = address(this);\r\n        uint256 cs;\r\n        assembly {\r\n            cs := extcodesize(_self)\r\n        }\r\n        return cs == 0;\r\n    }\r\n\r\n    // Reserved storage space to allow for layout changes in the future.\r\n    uint256[50] private ______gap;\r\n}\r\n\r\n\r\n/**\r\n * @title Ownable\r\n * @dev The Ownable contract has an owner address, and provides basic authorization control\r\n * functions, this simplifies the implementation of \"user permissions\".\r\n */\r\ncontract Ownable is Initializable {\r\n    address private _owner;\r\n\r\n    event OwnershipRenounced(address indexed previousOwner);\r\n    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);\r\n\r\n    /**\r\n     * @dev The Ownable constructor sets the original `owner` of the contract to the sender\r\n     * account.\r\n     */\r\n    function initialize(address sender) public virtual initializer {\r\n        _owner = sender;\r\n    }\r\n\r\n    /**\r\n     * @return the address of the owner.\r\n     */\r\n    function owner() public view returns (address) {\r\n        return _owner;\r\n    }\r\n\r\n    /**\r\n     * @dev Throws if called by any account other than the owner.\r\n     */\r\n    modifier onlyOwner() {\r\n        require(isOwner(), \"onlyOwner\");\r\n        _;\r\n    }\r\n\r\n    /**\r\n     * @return true if `msg.sender` is the owner of the contract.\r\n     */\r\n    function isOwner() public view returns (bool) {\r\n        return msg.sender == _owner;\r\n    }\r\n\r\n    /**\r\n     * @dev Allows the current owner to relinquish control of the contract.\r\n     * @notice Renouncing to ownership will leave the contract without an owner.\r\n     * It will not be possible to call the functions with the `onlyOwner`\r\n     * modifier anymore.\r\n     */\r\n    function renounceOwnership() public onlyOwner {\r\n        emit OwnershipRenounced(_owner);\r\n        _owner = address(0);\r\n    }\r\n\r\n    /**\r\n     * @dev Allows the current owner to transfer control of the contract to a newOwner.\r\n     * @param newOwner The address to transfer ownership to.\r\n     */\r\n    function transferOwnership(address newOwner) public onlyOwner {\r\n        _transferOwnership(newOwner);\r\n    }\r\n\r\n    /**\r\n     * @dev Transfers control of the contract to a newOwner.\r\n     * @param newOwner The address to transfer ownership to.\r\n     */\r\n    function _transferOwnership(address newOwner) internal {\r\n        require(newOwner != address(0), \"not address zero\");\r\n        emit OwnershipTransferred(_owner, newOwner);\r\n        _owner = newOwner;\r\n    }\r\n\r\n    uint256[50] private ______gap;\r\n}\r\n\r\n/*\r\nMIT License\r\n\r\nCopyright (c) 2018 requestnetwork\r\nCopyright (c) 2018 Fragments, Inc.\r\n\r\nPermission is hereby granted, free of charge, to any person obtaining a copy\r\nof this software and associated documentation files (the \"Software\"), to deal\r\nin the Software without restriction, including without limitation the rights\r\nto use, copy, modify, merge, publish, distribute, sublicense, and/or sell\r\ncopies of the Software, and to permit persons to whom the Software is\r\nfurnished to do so, subject to the following conditions:\r\n\r\nThe above copyright notice and this permission notice shall be included in all\r\ncopies or substantial portions of the Software.\r\n\r\nTHE SOFTWARE IS PROVIDED \"AS IS\", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR\r\nIMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,\r\nFITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE\r\nAUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER\r\nLIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,\r\nOUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE\r\nSOFTWARE.\r\n*/\r\n\r\n\r\n/**\r\n * @title SafeMathInt\r\n * @dev Math operations for int256 with overflow safety checks.\r\n */\r\nlibrary SafeMathInt {\r\n    int256 private constant MIN_INT256 = int256(1) << 255;\r\n    int256 private constant MAX_INT256 = ~(int256(1) << 255);\r\n\r\n    /**\r\n     * @dev Multiplies two int256 variables and fails on overflow.\r\n     */\r\n    function mul(int256 a, int256 b) internal pure returns (int256) {\r\n        int256 c = a * b;\r\n\r\n        // Detect overflow when multiplying MIN_INT256 with -1\r\n        require(c != MIN_INT256 || (a & MIN_INT256) != (b & MIN_INT256));\r\n        require((b == 0) || (c / b == a));\r\n        return c;\r\n    }\r\n\r\n    /**\r\n     * @dev Division of two int256 variables and fails on overflow.\r\n     */\r\n    function div(int256 a, int256 b) internal pure returns (int256) {\r\n        // Prevent overflow when dividing MIN_INT256 by -1\r\n        require(b != -1 || a != MIN_INT256);\r\n\r\n        // Solidity already throws when dividing by 0.\r\n        return a / b;\r\n    }\r\n\r\n    /**\r\n     * @dev Subtracts two int256 variables and fails on overflow.\r\n     */\r\n    function sub(int256 a, int256 b) internal pure returns (int256) {\r\n        int256 c = a - b;\r\n        require((b >= 0 && c <= a) || (b < 0 && c > a));\r\n        return c;\r\n    }\r\n\r\n    /**\r\n     * @dev Adds two int256 variables and fails on overflow.\r\n     */\r\n    function add(int256 a, int256 b) internal pure returns (int256) {\r\n        int256 c = a + b;\r\n        require((b >= 0 && c >= a) || (b < 0 && c < a));\r\n        return c;\r\n    }\r\n\r\n    /**\r\n     * @dev Converts to absolute value, and fails on overflow.\r\n     */\r\n    function abs(int256 a) internal pure returns (int256) {\r\n        require(a != MIN_INT256);\r\n        return a < 0 ? -a : a;\r\n    }\r\n}\r\n\r\n/**\r\n * @title Various utilities useful for uint256.\r\n */\r\nlibrary UInt256Lib {\r\n    uint256 private constant MAX_INT256 = ~(uint256(1) << 255);\r\n\r\n    /**\r\n     * @dev Safely converts a uint256 to an int256.\r\n     */\r\n    function toInt256Safe(uint256 a) internal pure returns (int256) {\r\n        require(a <= MAX_INT256);\r\n        return int256(a);\r\n    }\r\n}\r\n\r\n\r\n\r\ninterface ITBill {\r\n    function totalSupply() external view returns (uint256);\r\n    function rebase(uint256 epoch, int256 supplyDelta) external returns (uint256);    \r\n    function scale() external view returns (uint256);\r\n}\r\n\r\ninterface IOracle {\r\n    function getData() external view returns (uint256, bool);\r\n}\r\n\r\n/**\r\n * @title TBill Monetary Supply Policy\r\n * @dev This is an implementation of the TBill Ideal Money protocol.\r\n *      TBill operates symmetrically on expansion and contraction. It will both split and\r\n *      combine coins to maintain a stable unit price.\r\n *\r\n *      This component regulates the token supply of the TBill ERC20 token in response to\r\n *      market oracles.\r\n */\r\ncontract TBillPolicy is Ownable {\r\n    using SafeMath for uint256;\r\n    using SafeMathInt for int256;\r\n    using UInt256Lib for uint256;\r\n\r\n    event LogRebase(\r\n        uint256 indexed epoch,\r\n        uint256 exchangeRate,\r\n        uint256 cpi,\r\n        int256 requestedSupplyAdjustment,\r\n        uint256 timestampSec\r\n    );\r\n\r\n    ITBill public uFrags;\r\n\r\n    // Provides the current CPI, as an 18 decimal fixed point number.\r\n    IOracle public cpiOracle;\r\n\r\n    // Market oracle provides the token/USD exchange rate as an 18 decimal fixed point number.\r\n    // (eg) An oracle value of 1.5e18 it would mean 1 Ample is trading for $1.50.\r\n    IOracle public marketOracle;\r\n\r\n    // CPI value at the time of launch, as an 18 decimal fixed point number.\r\n    uint256 private baseCpi;\r\n\r\n    // If the current exchange rate is within this fractional distance from the target, no supply\r\n    // update is performed. Fixed point number--same format as the rate.\r\n    // (ie) abs(rate - targetRate) / targetRate < deviationThreshold, then no supply change.\r\n    // DECIMALS Fixed point number.\r\n    uint256 public deviationThreshold;\r\n\r\n    // The rebase lag parameter, used to dampen the applied supply adjustment by 1 / rebaseLag\r\n    // Check setRebaseLag comments for more details.\r\n    // Natural number, no decimal places.\r\n    uint256 public rebaseLag;\r\n\r\n    // More than this much time must pass between rebase operations.\r\n    uint256 public minRebaseTimeIntervalSec;\r\n\r\n    // Block timestamp of last rebase operation\r\n    uint256 public lastRebaseTimestampSec;\r\n\r\n    // The rebase window begins this many seconds into the minRebaseTimeInterval period.\r\n    // For example if minRebaseTimeInterval is 24hrs, it represents the time of day in seconds.\r\n    uint256 public rebaseWindowOffsetSec;\r\n\r\n    // The length of the time window where a rebase operation is allowed to execute, in seconds.\r\n    uint256 public rebaseWindowLengthSec;\r\n\r\n    // The number of rebase cycles since inception\r\n    uint256 public epoch;\r\n\r\n    uint256 private constant DECIMALS = 18;\r\n\r\n    // Due to the expression in computeSupplyDelta(), MAX_RATE * MAX_SUPPLY must fit into an int256.\r\n    // Both are 18 decimals fixed point numbers.\r\n    uint256 private constant MAX_RATE = 10**6 * 10**DECIMALS;\r\n    // MAX_SUPPLY = MAX_INT256 / MAX_RATE\r\n    uint256 private constant MAX_SUPPLY = uint256(type(int256).max) / MAX_RATE;\r\n\r\n    // This module orchestrates the rebase execution and downstream notification.\r\n    address public orchestrator;\r\n\r\n    modifier onlyOrchestrator() {\r\n        require(msg.sender == orchestrator, \"msg.sender != orchestrator\");\r\n        _;\r\n    }\r\n\r\n    /**\r\n     * @notice Initiates a new rebase operation, provided the minimum time period has elapsed.\r\n     *\r\n     * @dev The supply adjustment equals (_totalSupply * DeviationFromTargetRate) / rebaseLag\r\n     *      Where DeviationFromTargetRate is (MarketOracleRate - targetRate) / targetRate\r\n     *      and targetRate is CpiOracleRate / baseCpi\r\n     */\r\n    function rebase() external onlyOrchestrator {\r\n        require(inRebaseWindow(), \"not inRebaseWindow\");\r\n\r\n        // This comparison also ensures there is no reentrancy.\r\n        require(\r\n            lastRebaseTimestampSec.add(minRebaseTimeIntervalSec) < block.timestamp,\r\n            \"not enough time for rebase\"\r\n        );\r\n\r\n        // Snap the rebase time to the start of this window.\r\n        lastRebaseTimestampSec = block\r\n            .timestamp\r\n            .sub(block.timestamp.mod(minRebaseTimeIntervalSec))\r\n            .add(rebaseWindowOffsetSec);\r\n\r\n        epoch = epoch.add(1);\r\n\r\n        uint256 cpi;\r\n        bool cpiValid;\r\n        (cpi, cpiValid) = cpiOracle.getData();\r\n        require(cpiValid, \"not cpiValid\");\r\n\r\n        uint256 targetRate = cpi.mul(10**DECIMALS).div(baseCpi);\r\n\r\n        uint256 exchangeRate;\r\n        bool rateValid;\r\n        (exchangeRate, rateValid) = marketOracle.getData();\r\n        require(rateValid, \"not rateValid\");\r\n\r\n        if (exchangeRate > MAX_RATE) {\r\n            exchangeRate = MAX_RATE;\r\n        }\r\n\r\n        int256 supplyDelta = computeSupplyDelta(exchangeRate, targetRate);\r\n\r\n        // Apply the Dampening factor.\r\n        supplyDelta = supplyDelta.div(rebaseLag.toInt256Safe());\r\n\r\n        if (supplyDelta > 0 && uFrags.totalSupply().add(uint256(supplyDelta)) > MAX_SUPPLY) {\r\n            supplyDelta = (MAX_SUPPLY.sub(uFrags.totalSupply())).toInt256Safe();\r\n        }\r\n\r\n        uint256 supplyAfterRebase = uFrags.rebase(epoch, supplyDelta);\r\n        assert(supplyAfterRebase <= MAX_SUPPLY);\r\n        emit LogRebase(epoch, exchangeRate, cpi, supplyDelta, block.timestamp);\r\n    }\r\n\r\n    /**\r\n     * @notice Sets the reference to the CPI oracle.\r\n     * @param cpiOracle_ The address of the cpi oracle contract.\r\n     */\r\n    function setCpiOracle(IOracle cpiOracle_) external onlyOwner {\r\n        cpiOracle = cpiOracle_;\r\n    }\r\n\r\n    /**\r\n     * @notice Sets the reference to the market oracle.\r\n     * @param marketOracle_ The address of the market oracle contract.\r\n     */\r\n    function setMarketOracle(IOracle marketOracle_) external onlyOwner {\r\n        marketOracle = marketOracle_;\r\n    }\r\n\r\n    /**\r\n     * @notice Sets the reference to the orchestrator.\r\n     * @param orchestrator_ The address of the orchestrator contract.\r\n     */\r\n    function setOrchestrator(address orchestrator_) external onlyOwner {\r\n        orchestrator = orchestrator_;\r\n    }\r\n\r\n    /**\r\n     * @notice Sets the deviation threshold fraction. If the exchange rate given by the market\r\n     *         oracle is within this fractional distance from the targetRate, then no supply\r\n     *         modifications are made. DECIMALS fixed point number.\r\n     * @param deviationThreshold_ The new exchange rate threshold fraction.\r\n     */\r\n    function setDeviationThreshold(uint256 deviationThreshold_) external onlyOwner {\r\n        deviationThreshold = deviationThreshold_;\r\n    }\r\n\r\n    /**\r\n     * @notice Sets the rebase lag parameter.\r\n               It is used to dampen the applied supply adjustment by 1 / rebaseLag\r\n               If the rebase lag R, equals 1, the smallest value for R, then the full supply\r\n               correction is applied on each rebase cycle.\r\n               If it is greater than 1, then a correction of 1/R of is applied on each rebase.\r\n     * @param rebaseLag_ The new rebase lag parameter.\r\n     */\r\n    function setRebaseLag(uint256 rebaseLag_) external onlyOwner {\r\n        require(rebaseLag_ > 0, \"invalid rebaseLag\");\r\n        rebaseLag = rebaseLag_;\r\n    }\r\n\r\n    /**\r\n     * @notice Sets the parameters which control the timing and frequency of\r\n     *         rebase operations.\r\n     *         a) the minimum time period that must elapse between rebase cycles.\r\n     *         b) the rebase window offset parameter.\r\n     *         c) the rebase window length parameter.\r\n     * @param minRebaseTimeIntervalSec_ More than this much time must pass between rebase\r\n     *        operations, in seconds.\r\n     * @param rebaseWindowOffsetSec_ The number of seconds from the beginning of\r\n              the rebase interval, where the rebase window begins.\r\n     * @param rebaseWindowLengthSec_ The length of the rebase window in seconds.\r\n     */\r\n    function setRebaseTimingParameters(\r\n        uint256 minRebaseTimeIntervalSec_,\r\n        uint256 rebaseWindowOffsetSec_,\r\n        uint256 rebaseWindowLengthSec_\r\n    ) external onlyOwner {\r\n        require(minRebaseTimeIntervalSec_ > 0, \"no minRebaseTimeIntervalSec set\");\r\n        require(\r\n            rebaseWindowOffsetSec_ < minRebaseTimeIntervalSec_,\r\n            \"offset >= interval\"\r\n        );\r\n\r\n        minRebaseTimeIntervalSec = minRebaseTimeIntervalSec_;\r\n        rebaseWindowOffsetSec = rebaseWindowOffsetSec_;\r\n        rebaseWindowLengthSec = rebaseWindowLengthSec_;\r\n    }\r\n\r\n    /**\r\n     * @notice A multi-chain AMPL interface method. The Ampleforth monetary policy contract\r\n     *         on the base-chain and XC-AmpleController contracts on the satellite-chains\r\n     *         implement this method. It atomically returns two values:\r\n     *         what the current contract believes to be,\r\n     *         the globalEpoch and globalScale.\r\n     * @return globalEpoch The current epoch number.\r\n     * @return globalScale The current scale (total scaled supply / total supply)\r\n     */\r\n    function globalEpochAndScale() external view returns (uint256, uint256) {\r\n        return (epoch, uFrags.scale());\r\n    }\r\n\r\n    /**\r\n     * @dev ZOS upgradable contract initialization method.\r\n     *      It is called at the time of contract creation to invoke parent class initializers and\r\n     *      initialize the contract's state variables.\r\n     */\r\n    function initialize(\r\n        address owner_,\r\n        ITBill uFrags_,\r\n        uint256 baseCpi_\r\n    ) public initializer {\r\n        Ownable.initialize(owner_);\r\n\r\n        deviationThreshold = 5 * 10**(DECIMALS - 2);\r\n        rebaseLag = 30;\r\n        minRebaseTimeIntervalSec = 1 days;\r\n        rebaseWindowOffsetSec = 72000; // 8PM UTC\r\n        rebaseWindowLengthSec = 15 minutes;\r\n        lastRebaseTimestampSec = 0;\r\n        epoch = 0;\r\n\r\n        uFrags = uFrags_;\r\n        baseCpi = baseCpi_;\r\n    }\r\n\r\n    /**\r\n     * @return If the latest block timestamp is within the rebase time window it, returns true.\r\n     *         Otherwise, returns false.\r\n     */\r\n    function inRebaseWindow() public view returns (bool) {\r\n        return (block.timestamp.mod(minRebaseTimeIntervalSec) >= rebaseWindowOffsetSec &&\r\n            block.timestamp.mod(minRebaseTimeIntervalSec) <\r\n            (rebaseWindowOffsetSec.add(rebaseWindowLengthSec)));\r\n    }\r\n\r\n    /**\r\n     * @return Computes the total supply adjustment in response to the exchange rate\r\n     *         and the targetRate.\r\n     */\r\n    function computeSupplyDelta(uint256 rate, uint256 targetRate) internal view returns (int256) {\r\n        if (withinDeviationThreshold(rate, targetRate)) {\r\n            return 0;\r\n        }\r\n\r\n        // supplyDelta = totalSupply * (rate - targetRate) / targetRate\r\n        int256 targetRateSigned = targetRate.toInt256Safe();\r\n        return\r\n            uFrags.totalSupply().toInt256Safe().mul(rate.toInt256Safe().sub(targetRateSigned)).div(\r\n                targetRateSigned\r\n            );\r\n    }\r\n\r\n    /**\r\n     * @param rate The current exchange rate, an 18 decimal fixed point number.\r\n     * @param targetRate The target exchange rate, an 18 decimal fixed point number.\r\n     * @return If the rate is within the deviation threshold from the target rate, returns true.\r\n     *         Otherwise, returns false.\r\n     */\r\n    function withinDeviationThreshold(uint256 rate, uint256 targetRate)\r\n        internal\r\n        view\r\n        returns (bool)\r\n    {\r\n        uint256 absoluteDeviationThreshold = targetRate.mul(deviationThreshold).div(10**DECIMALS);\r\n\r\n        return\r\n            (rate >= targetRate && rate.sub(targetRate) < absoluteDeviationThreshold) ||\r\n            (rate < targetRate && targetRate.sub(rate) < absoluteDeviationThreshold);\r\n    }\r\n}"
    }
  }
}
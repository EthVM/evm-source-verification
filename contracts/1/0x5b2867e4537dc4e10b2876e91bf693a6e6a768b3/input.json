{
  "language": "Solidity",
  "settings": {
    "libraries": {},
    "metadata": {
      "useLiteralContent": true
    },
    "optimizer": {
      "enabled": true,
      "runs": 200
    },
    "remappings": [],
    "outputSelection": {
      "*": {
        "*": [
          "*"
        ],
        "": [
          "*"
        ]
      }
    }
  },
  "sources": {
    "browser/DssExecLib1.sol": {
      "content": "// SPDX-License-Identifier: AGPL-3.0-or-later\n// hevm: flattened sources of src/DssExecLib.sol\npragma solidity >=0.6.11 <0.7.0;\n\n////// src/MathLib.sol\n//\n// MathLib.sol -- Math Functions\n//\n// Copyright (C) 2020 Maker Ecosystem Growth Holdings, Inc.\n//\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU Affero General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n//\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU Affero General Public License for more details.\n//\n// You should have received a copy of the GNU Affero General Public License\n// along with this program.  If not, see <https://www.gnu.org/licenses/>.\n/* pragma solidity ^0.6.11; */\n\nlibrary MathLib {\n\n    uint256 constant public WAD      = 10 ** 18;\n    uint256 constant public RAY      = 10 ** 27;\n    uint256 constant public RAD      = 10 ** 45;\n    uint256 constant public THOUSAND = 10 ** 3;\n    uint256 constant public MILLION  = 10 ** 6;\n\n    // --- SafeMath Functions ---\n    function add(uint x, uint y) internal pure returns (uint z) {\n        require((z = x + y) >= x);\n    }\n    function sub(uint x, uint y) internal pure returns (uint z) {\n        require((z = x - y) <= x);\n    }\n    function mul(uint x, uint y) internal pure returns (uint z) {\n        require(y == 0 || (z = x * y) / y == x);\n    }\n    function wmul(uint x, uint y) internal pure returns (uint z) {\n        z = add(mul(x, y), WAD / 2) / WAD;\n    }\n    function rmul(uint x, uint y) internal pure returns (uint z) {\n        z = add(mul(x, y), RAY / 2) / RAY;\n    }\n    function wdiv(uint x, uint y) internal pure returns (uint z) {\n        z = add(mul(x, WAD), y / 2) / y;\n    }\n    function rdiv(uint x, uint y) internal pure returns (uint z) {\n        z = add(mul(x, RAY), y / 2) / y;\n    }\n}\n\n////// src/DssExecLib.sol\n//\n// DssExecLib.sol -- MakerDAO Executive Spellcrafting Library\n//\n// Copyright (C) 2020 Maker Ecosystem Growth Holdings, Inc.\n//\n// This program is free software: you can redistribute it and/or modify\n// it under the terms of the GNU Affero General Public License as published by\n// the Free Software Foundation, either version 3 of the License, or\n// (at your option) any later version.\n//\n// This program is distributed in the hope that it will be useful,\n// but WITHOUT ANY WARRANTY; without even the implied warranty of\n// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n// GNU Affero General Public License for more details.\n//\n// You should have received a copy of the GNU Affero General Public License\n// along with this program.  If not, see <https://www.gnu.org/licenses/>.\n/* pragma solidity ^0.6.11; */\n\n/* import \"./MathLib.sol\"; */\n\ninterface Initializable {\n    function init(bytes32) external;\n}\n\ninterface Authorizable {\n    function rely(address) external;\n    function deny(address) external;\n}\n\ninterface Fileable {\n    function file(bytes32, address) external;\n    function file(bytes32, uint256) external;\n    function file(bytes32, bytes32, uint256) external;\n    function file(bytes32, bytes32, address) external;\n}\n\ninterface Drippable {\n    function drip() external returns (uint256);\n    function drip(bytes32) external returns (uint256);\n}\n\ninterface Pricing {\n    function poke(bytes32) external;\n}\n\ninterface ERC20 {\n    function decimals() external returns (uint8);\n}\n\ninterface DssVat {\n    function ilks(bytes32) external returns (uint256 Art, uint256 rate, uint256 spot, uint256 line, uint256 dust);\n    function Line() external view returns (uint256);\n}\n\ninterface AuctionLike {\n    function vat() external returns (address);\n    function cat() external returns (address); // Only flip\n    function beg() external returns (uint256);\n    function pad() external returns (uint256); // Only flop\n    function ttl() external returns (uint256);\n    function tau() external returns (uint256);\n    function ilk() external returns (bytes32); // Only flip\n    function gem() external returns (bytes32); // Only flap/flop\n}\n\ninterface JoinLike_2 {\n    function vat() external returns (address);\n    function ilk() external returns (bytes32);\n    function gem() external returns (address);\n    function dec() external returns (uint256);\n}\n\n// Includes Median and OSM functions\ninterface OracleLike_2 {\n    function src() external view returns (address);\n    function lift(address[] calldata) external;\n    function drop(address[] calldata) external;\n    function setBar(uint256) external;\n    function kiss(address) external;\n    function diss(address) external;\n    function kiss(address[] calldata) external;\n    function diss(address[] calldata) external;\n}\n\ninterface MomLike {\n    function setOsm(bytes32, address) external;\n}\n\ninterface RegistryLike_2 {\n    function add(address) external;\n    function info(bytes32) external view returns (\n        string memory, string memory, uint256, address, address, address, address\n    );\n}\n\n// https://github.com/makerdao/dss-chain-log\ninterface ChainlogLike_2 {\n    function setVersion(string calldata) external;\n    function setIPFS(string calldata) external;\n    function setSha256sum(string calldata) external;\n    function setAddress(bytes32, address) external;\n    function removeAddress(bytes32) external;\n}\n\ninterface IAMLike {\n    function ilks(bytes32) external view returns (uint256,uint256,uint48,uint48,uint48);\n    function setIlk(bytes32,uint256,uint256,uint256) external;\n    function remIlk(bytes32) external;\n    function exec(bytes32) external returns (uint256);\n}\n\n\ncontract DssExecLib {\n\n    using MathLib for *;\n\n    /****************************/\n    /*** Changelog Management ***/\n    /****************************/\n    /**\n        @dev Set an address in the MCD on-chain changelog.\n        @param _log Address of the chainlog contract\n        @param _key Access key for the address (e.g. \"MCD_VAT\")\n        @param _val The address associated with the _key\n    */\n    function setChangelogAddress(address _log, bytes32 _key, address _val) public {\n        ChainlogLike_2(_log).setAddress(_key, _val);\n    }\n\n    /**\n        @dev Set version in the MCD on-chain changelog.\n        @param _log Address of the chainlog contract\n        @param _version Changelog version (e.g. \"1.1.2\")\n    */\n    function setChangelogVersion(address _log, string memory _version) public {\n        ChainlogLike_2(_log).setVersion(_version);\n    }\n    /**\n        @dev Set IPFS hash of IPFS changelog in MCD on-chain changelog.\n        @param _log Address of the chainlog contract\n        @param _ipfsHash IPFS hash (e.g. \"QmefQMseb3AiTapiAKKexdKHig8wroKuZbmLtPLv4u2YwW\")\n    */\n    function setChangelogIPFS(address _log, string memory _ipfsHash) public {\n        ChainlogLike_2(_log).setIPFS(_ipfsHash);\n    }\n    /**\n        @dev Set SHA256 hash in MCD on-chain changelog.\n        @param _log Address of the chainlog contract\n        @param _SHA256Sum SHA256 hash (e.g. \"e42dc9d043a57705f3f097099e6b2de4230bca9a020c797508da079f9079e35b\")\n    */\n    function setChangelogSHA256(address _log, string memory _SHA256Sum) public {\n        ChainlogLike_2(_log).setSha256sum(_SHA256Sum);\n    }\n\n\n    /**********************/\n    /*** Authorizations ***/\n    /**********************/\n    /**\n        @dev Give an address authorization to perform auth actions on the contract.\n        @param _base   The address of the contract where the authorization will be set\n        @param _ward   Address to be authorized\n    */\n    function authorize(address _base, address _ward) public {\n        Authorizable(_base).rely(_ward);\n    }\n    /**\n        @dev Revoke contract authorization from an address.\n        @param _base   The address of the contract where the authorization will be revoked\n        @param _ward   Address to be deauthorized\n    */\n    function deauthorize(address _base, address _ward) public {\n        Authorizable(_base).deny(_ward);\n    }\n\n    /**************************/\n    /*** Accumulating Rates ***/\n    /**************************/\n    /**\n        @dev Update rate accumulation for the Dai Savings Rate (DSR).\n        @param _pot   Address of the MCD_POT core contract\n    */\n    function accumulateDSR(address _pot) public {\n        Drippable(_pot).drip();\n    }\n    /**\n        @dev Update rate accumulation for the stability fees of a given collateral type.\n        @param _jug   Address of the MCD_JUG core contract\n        @param _ilk   Collateral type\n    */\n    function accumulateCollateralStabilityFees(address _jug, bytes32 _ilk) public {\n        Drippable(_jug).drip(_ilk);\n    }\n\n    /*********************/\n    /*** Price Updates ***/\n    /*********************/\n    /**\n        @dev Update price of a given collateral type.\n        @param _spot  Spotter contract address\n        @param _ilk   Collateral type\n    */\n    function updateCollateralPrice(address _spot, bytes32 _ilk) public {\n        Pricing(_spot).poke(_ilk);\n    }\n\n    /****************************/\n    /*** System Configuration ***/\n    /****************************/\n    /**\n        @dev Set a contract in another contract, defining the relationship (ex. set a new Cat contract in the Vat)\n        @param _base   The address of the contract where the new contract address will be filed\n        @param _what   Name of contract to file\n        @param _addr   Address of contract to file\n    */\n    function setContract(address _base, bytes32 _what, address _addr) public {\n        Fileable(_base).file(_what, _addr);\n    }\n    /**\n        @dev Set a contract in another contract, defining the relationship (ex. set a new Cat contract in the Vat)\n        @param _base   The address of the contract where the new contract address will be filed\n        @param _ilk    Collateral type\n        @param _what   Name of contract to file\n        @param _addr   Address of contract to file\n    */\n    function setContract(address _base, bytes32 _ilk, bytes32 _what, address _addr) public {\n        Fileable(_base).file(_ilk, _what, _addr);\n    }\n\n    /******************************/\n    /*** System Risk Parameters ***/\n    /******************************/\n    // function setGlobalDebtCeiling(uint256 _amount) public { setGlobalDebtCeiling(vat(), _amount); }\n    /**\n        @dev Set the global debt ceiling. Amount will be converted to the correct internal precision.\n        @param _vat    The address of the Vat core accounting contract\n        @param _amount The amount to set in DAI (ex. 10m DAI amount == 10000000)\n    */\n    function setGlobalDebtCeiling(address _vat, uint256 _amount) public {\n        require(_amount < MathLib.WAD);  // \"LibDssExec/incorrect-global-Line-precision\"\n        Fileable(_vat).file(\"Line\", _amount * MathLib.RAD);\n    }\n    /**\n        @dev Increase the global debt ceiling by a specific amount. Amount will be converted to the correct internal precision.\n        @param _vat    The address of the Vat core accounting contract\n        @param _amount The amount to add in DAI (ex. 10m DAI amount == 10000000)\n    */\n    function increaseGlobalDebtCeiling(address _vat, uint256 _amount) public {\n        setGlobalDebtCeiling(_vat, MathLib.add(DssVat(_vat).Line() / MathLib.RAD, _amount));\n    }\n    /**\n        @dev Decrease the global debt ceiling by a specific amount. Amount will be converted to the correct internal precision.\n        @param _vat    The address of the Vat core accounting contract\n        @param _amount The amount to reduce in DAI (ex. 10m DAI amount == 10000000)\n    */\n    function decreaseGlobalDebtCeiling(address _vat, uint256 _amount) public {\n        setGlobalDebtCeiling(_vat, MathLib.sub(DssVat(_vat).Line() / MathLib.RAD, _amount));\n    }\n    /**\n        @dev Set the Dai Savings Rate. See: docs/rates.txt\n        @param _pot    The address of the Pot core contract\n        @param _rate   The accumulated rate (ex. 4% => 1000000001243680656318820312)\n    */\n    function setDSR(address _pot, uint256 _rate) public {\n        require((_rate >= MathLib.RAY) && (_rate < 2 * MathLib.RAY));  // \"LibDssExec/dsr-out-of-bounds\"\n        Fileable(_pot).file(\"dsr\", _rate);\n    }\n    /**\n        @dev Set the DAI amount for system surplus auctions. Amount will be converted to the correct internal precision.\n        @param _vow    The address of the Vow core contract\n        @param _amount The amount to set in DAI (ex. 10m DAI amount == 10000000)\n    */\n    function setSurplusAuctionAmount(address _vow, uint256 _amount) public {\n        require(_amount < MathLib.WAD);  // \"LibDssExec/incorrect-vow-bump-precision\"\n        Fileable(_vow).file(\"bump\", _amount * MathLib.RAD);\n    }\n    /**\n        @dev Set the DAI amount for system surplus buffer, must be exceeded before surplus auctions start. Amount will be converted to the correct internal precision.\n        @param _vow    The address of the Vow core contract\n        @param _amount The amount to set in DAI (ex. 10m DAI amount == 10000000)\n    */\n    function setSurplusBuffer(address _vow, uint256 _amount) public {\n        require(_amount < MathLib.WAD);  // \"LibDssExec/incorrect-vow-hump-precision\"\n        Fileable(_vow).file(\"hump\", _amount * MathLib.RAD);\n    }\n    /**\n        @dev Set minimum bid increase for surplus auctions. Amount will be converted to the correct internal precision.\n        @dev Equation used for conversion is (1 + pct / 10,000) * WAD\n        @param _flap    The address of the Flapper core contract\n        @param _pct_bps The pct, in basis points, to set in integer form (x100). (ex. 5% = 5 * 100 = 500)\n    */\n    function setMinSurplusAuctionBidIncrease(address _flap, uint256 _pct_bps) public {\n        require(_pct_bps < 10 * MathLib.THOUSAND);  // \"LibDssExec/incorrect-flap-beg-precision\"\n        Fileable(_flap).file(\"beg\", MathLib.add(MathLib.WAD, MathLib.wdiv(_pct_bps, 10 * MathLib.THOUSAND)));\n    }\n    /**\n        @dev Set bid duration for surplus auctions.\n        @param _flap   The address of the Flapper core contract\n        @param _duration Amount of time for bids.\n    */\n    function setSurplusAuctionBidDuration(address _flap, uint256 _duration) public {\n        Fileable(_flap).file(\"ttl\", _duration);\n    }\n    /**\n        @dev Set total auction duration for surplus auctions.\n        @param _flap   The address of the Flapper core contract\n        @param _duration Amount of time for auctions.\n    */\n    function setSurplusAuctionDuration(address _flap, uint256 _duration) public {\n        Fileable(_flap).file(\"tau\", _duration);\n    }\n    /**\n        @dev Set the number of seconds that pass before system debt is auctioned for MKR tokens.\n        @param _vow    The address of the Vow core contract\n        @param _duration Duration in seconds\n    */\n    function setDebtAuctionDelay(address _vow, uint256 _duration) public {\n        Fileable(_vow).file(\"wait\", _duration);\n    }\n    /**\n        @dev Set the DAI amount for system debt to be covered by each debt auction. Amount will be converted to the correct internal precision.\n        @param _vow    The address of the Vow core contract\n        @param _amount The amount to set in DAI (ex. 10m DAI amount == 10000000)\n    */\n    function setDebtAuctionDAIAmount(address _vow, uint256 _amount) public {\n        require(_amount < MathLib.WAD);  // \"LibDssExec/incorrect-vow-sump-precision\"\n        Fileable(_vow).file(\"sump\", _amount * MathLib.RAD);\n    }\n    /**\n        @dev Set the starting MKR amount to be auctioned off to cover system debt in debt auctions. Amount will be converted to the correct internal precision.\n        @param _vow    The address of the Vow core contract\n        @param _amount The amount to set in MKR (ex. 250 MKR amount == 250)\n    */\n    function setDebtAuctionMKRAmount(address _vow, uint256 _amount) public {\n        require(_amount < MathLib.WAD);  // \"LibDssExec/incorrect-vow-dump-precision\"\n        Fileable(_vow).file(\"dump\", _amount * MathLib.WAD);\n    }\n    /**\n        @dev Set minimum bid increase for debt auctions. Amount will be converted to the correct internal precision.\n        @dev Equation used for conversion is (1 + pct / 10,000) * WAD\n        @param _flop   The address of the Flopper core contract\n        @param _pct_bps    The pct, in basis points, to set in integer form (x100). (ex. 5% = 5 * 100 = 500)\n    */\n    function setMinDebtAuctionBidIncrease(address _flop, uint256 _pct_bps) public {\n        require(_pct_bps < 10 * MathLib.THOUSAND);  // \"LibDssExec/incorrect-flap-beg-precision\"\n        Fileable(_flop).file(\"beg\", MathLib.add(MathLib.WAD, MathLib.wdiv(_pct_bps, 10 * MathLib.THOUSAND)));\n    }\n    /**\n        @dev Set bid duration for debt auctions.\n        @param _flop   The address of the Flopper core contract\n        @param _duration Amount of time for bids.\n    */\n    function setDebtAuctionBidDuration(address _flop, uint256 _duration) public {\n        Fileable(_flop).file(\"ttl\", _duration);\n    }\n    /**\n        @dev Set total auction duration for debt auctions.\n        @param _flop   The address of the Flopper core contract\n        @param _duration Amount of time for auctions.\n    */\n    function setDebtAuctionDuration(address _flop, uint256 _duration) public {\n        Fileable(_flop).file(\"tau\", _duration);\n    }\n    /**\n        @dev Set the rate of increasing amount of MKR out for auction during debt auctions. Amount will be converted to the correct internal precision.\n        @dev MKR amount is increased by this rate every \"tick\" (if auction duration has passed and no one has bid on the MKR)\n        @dev Equation used for conversion is (1 + pct / 10,000) * WAD\n        @param _flop   The address of the Flopper core contract\n        @param _pct_bps    The pct, in basis points, to set in integer form (x100). (ex. 5% = 5 * 100 = 500)\n    */\n    function setDebtAuctionMKRIncreaseRate(address _flop, uint256 _pct_bps) public {\n        Fileable(_flop).file(\"pad\", MathLib.add(MathLib.WAD, MathLib.wdiv(_pct_bps, 10 * MathLib.THOUSAND)));\n    }\n    /**\n        @dev Set the maximum total DAI amount that can be out for liquidation in the system at any point. Amount will be converted to the correct internal precision.\n        @param _cat    The address of the Cat core contract\n        @param _amount The amount to set in DAI (ex. 250,000 DAI amount == 250000)\n    */\n    function setMaxTotalDAILiquidationAmount(address _cat, uint256 _amount) public {\n        require(_amount < MathLib.WAD);  // \"LibDssExec/incorrect-vow-dump-precision\"\n        Fileable(_cat).file(\"box\", _amount * MathLib.RAD);\n    }\n    /**\n        @dev Set the duration of time that has to pass during emergency shutdown before collateral can start being claimed by DAI holders.\n        @param _end    The address of the End core contract\n        @param _duration Time in seconds to set for ES processing time\n    */\n    function setEmergencyShutdownProcessingTime(address _end, uint256 _duration) public {\n        Fileable(_end).file(\"wait\", _duration);\n    }\n    /**\n        @dev Set the global stability fee (is not typically used, currently is 0).\n            Many of the settings that change weekly rely on the rate accumulator\n            described at https://docs.makerdao.com/smart-contract-modules/rates-module\n            To check this yourself, use the following rate calculation (example 8%):\n\n            $ bc -l <<< 'scale=27; e( l(1.08)/(60 * 60 * 24 * 365) )'\n\n            A table of rates can also be found at:\n            https://ipfs.io/ipfs/QmefQMseb3AiTapiAKKexdKHig8wroKuZbmLtPLv4u2YwW\n        @param _jug    The address of the Jug core accounting contract\n        @param _rate   The accumulated rate (ex. 4% => 1000000001243680656318820312)\n    */\n    function setGlobalStabilityFee(address _jug, uint256 _rate) public {\n        require((_rate >= MathLib.RAY) && (_rate < 2 * MathLib.RAY));  // \"LibDssExec/global-stability-fee-out-of-bounds\"\n        Fileable(_jug).file(\"base\", _rate);\n    }\n    /**\n        @dev Set the value of DAI in the reference asset (e.g. $1 per DAI). Value will be converted to the correct internal precision.\n        @dev Equation used for conversion is value * RAY / 1000\n        @param _spot   The address of the Spot core contract\n        @param _value The value to set as integer (x1000) (ex. $1.025 == 1025)\n    */\n    function setDAIReferenceValue(address _spot, uint256 _value) public {\n        require(_value < MathLib.WAD);  // \"LibDssExec/incorrect-ilk-dunk-precision\"\n        Fileable(_spot).file(\"par\", MathLib.rdiv(_value, 1000));\n    }\n\n    /*****************************/\n    /*** Collateral Management ***/\n    /*****************************/\n    /**\n        @dev Set a collateral debt ceiling. Amount will be converted to the correct internal precision.\n        @param _vat    The address of the Vat core accounting contract\n        @param _ilk    The ilk to update (ex. bytes32(\"ETH-A\"))\n        @param _amount The amount to set in DAI (ex. 10m DAI amount == 10000000)\n    */\n    function setIlkDebtCeiling(address _vat, bytes32 _ilk, uint256 _amount) public {\n        require(_amount < MathLib.WAD);  // \"LibDssExec/incorrect-ilk-line-precision\"\n        Fileable(_vat).file(_ilk, \"line\", _amount * MathLib.RAD);\n    }\n    /**\n        @dev Increase a collateral debt ceiling. Amount will be converted to the correct internal precision.\n        @param _vat    The address of the Vat core accounting contract\n        @param _ilk    The ilk to update (ex. bytes32(\"ETH-A\"))\n        @param _amount The amount to increase in DAI (ex. 10m DAI amount == 10000000)\n        @param _global If true, increases the global debt ceiling by _amount\n    */\n    function increaseIlkDebtCeiling(address _vat, bytes32 _ilk, uint256 _amount, bool _global) public {\n        (,,,uint256 line_,) = DssVat(_vat).ilks(_ilk);\n        setIlkDebtCeiling(_vat, _ilk, MathLib.add(line_ / MathLib.RAD, _amount));\n        if (_global) { increaseGlobalDebtCeiling(_vat, _amount); }\n    }\n    /**\n        @dev Decrease a collateral debt ceiling. Amount will be converted to the correct internal precision.\n        @param _vat    The address of the Vat core accounting contract\n        @param _ilk    The ilk to update (ex. bytes32(\"ETH-A\"))\n        @param _amount The amount to decrease in DAI (ex. 10m DAI amount == 10000000)\n        @param _global If true, decreases the global debt ceiling by _amount\n    */\n    function decreaseIlkDebtCeiling(address _vat, bytes32 _ilk, uint256 _amount, bool _global) public {\n        (,,,uint256 line_,) = DssVat(_vat).ilks(_ilk);\n        setIlkDebtCeiling(_vat, _ilk, MathLib.sub(line_ / MathLib.RAD, _amount));\n        if (_global) { decreaseGlobalDebtCeiling(_vat, _amount); }\n    }\n    /**\n        @dev Set the parameters for an ilk in the \"MCD_IAM_AUTO_LINE\" auto-line\n        @param _iam    The address of the Vat core accounting contract\n        @param _ilk    The ilk to update (ex. bytes32(\"ETH-A\"))\n        @param _amount The Maximum value (ex. 100m DAI amount == 100000000)\n        @param _gap    The amount of Dai per step (ex. 5m Dai == 5000000)\n        @param _ttl    The amount of time (in seconds)\n    */\n    function setIlkAutoLineParameters(address _iam, bytes32 _ilk, uint256 _amount, uint256 _gap, uint256 _ttl) public {\n        require(_amount < MathLib.WAD);  // \"LibDssExec/incorrect-auto-line-amount-precision\"\n        require(_gap < MathLib.WAD);  // \"LibDssExec/incorrect-auto-line-gap-precision\"\n        IAMLike(_iam).setIlk(_ilk, _amount * MathLib.RAD, _gap * MathLib.RAD, _ttl);\n    }\n    /**\n        @dev Set the debt ceiling for an ilk in the \"MCD_IAM_AUTO_LINE\" auto-line without updating the time values\n        @param _iam    The address of the Vat core accounting contract\n        @param _ilk    The ilk to update (ex. bytes32(\"ETH-A\"))\n        @param _amount The amount to decrease in DAI (ex. 10m DAI amount == 10000000)\n    */\n    function setIlkAutoLineDebtCeiling(address _iam, bytes32 _ilk, uint256 _amount) public {\n        (, uint256 gap, uint48 ttl,,) = IAMLike(_iam).ilks(_ilk);\n        require(gap != 0 && ttl != 0);  // \"LibDssExec/auto-line-not-configured\"\n        IAMLike(_iam).setIlk(_ilk, _amount * MathLib.RAD, uint256(gap), uint256(ttl));\n    }\n    /**\n        @dev Remove an ilk in the \"MCD_IAM_AUTO_LINE\" auto-line\n        @param _iam    The address of the MCD_IAM_AUTO_LINE core accounting contract\n        @param _ilk    The ilk to remove (ex. bytes32(\"ETH-A\"))\n    */\n    function removeIlkFromAutoLine(address _iam, bytes32 _ilk) public {\n        IAMLike(_iam).remIlk(_ilk);\n    }\n    /**\n        @dev Set a collateral minimum vault amount. Amount will be converted to the correct internal precision.\n        @param _vat    The address of the Vat core accounting contract\n        @param _ilk    The ilk to update (ex. bytes32(\"ETH-A\"))\n        @param _amount The amount to set in DAI (ex. 10m DAI amount == 10000000)\n    */\n    function setIlkMinVaultAmount(address _vat, bytes32 _ilk, uint256 _amount) public {\n        require(_amount < MathLib.WAD);  // \"LibDssExec/incorrect-ilk-dust-precision\"\n        Fileable(_vat).file(_ilk, \"dust\", _amount * MathLib.RAD);\n    }\n    /**\n        @dev Set a collateral liquidation penalty. Amount will be converted to the correct internal precision.\n        @dev Equation used for conversion is (1 + pct / 10,000) * WAD\n        @param _cat    The address of the Cat core accounting contract (will need to revisit for LIQ-2.0)\n        @param _ilk    The ilk to update (ex. bytes32(\"ETH-A\"))\n        @param _pct_bps    The pct, in basis points, to set in integer form (x100). (ex. 10.25% = 10.25 * 100 = 1025)\n    */\n    function setIlkLiquidationPenalty(address _cat, bytes32 _ilk, uint256 _pct_bps) public {\n        require(_pct_bps < 10 * MathLib.THOUSAND);  // \"LibDssExec/incorrect-ilk-chop-precision\"\n        Fileable(_cat).file(_ilk, \"chop\", MathLib.add(MathLib.WAD, MathLib.wdiv(_pct_bps, 10 * MathLib.THOUSAND)));\n    }\n    /**\n        @dev Set max DAI amount for liquidation per vault for collateral. Amount will be converted to the correct internal precision.\n        @param _cat    The address of the Cat core accounting contract (will need to revisit for LIQ-2.0)\n        @param _ilk    The ilk to update (ex. bytes32(\"ETH-A\"))\n        @param _amount The amount to set in DAI (ex. 10m DAI amount == 10000000)\n    */\n    function setIlkMaxLiquidationAmount(address _cat, bytes32 _ilk, uint256 _amount) public {\n        require(_amount < MathLib.WAD);  // \"LibDssExec/incorrect-ilk-dunk-precision\"\n        Fileable(_cat).file(_ilk, \"dunk\", _amount * MathLib.RAD);\n    }\n    /**\n        @dev Set a collateral liquidation ratio. Amount will be converted to the correct internal precision.\n        @dev Equation used for conversion is pct * RAY / 10,000\n        @param _spot   The address of the Spot core accounting contract\n        @param _ilk    The ilk to update (ex. bytes32(\"ETH-A\"))\n        @param _pct_bps    The pct, in basis points, to set in integer form (x100). (ex. 150% = 150 * 100 = 15000)\n    */\n    function setIlkLiquidationRatio(address _spot, bytes32 _ilk, uint256 _pct_bps) public {\n        require(_pct_bps < 100 * MathLib.THOUSAND);  // \"LibDssExec/incorrect-ilk-mat-precision\" // Fails if pct >= 1000%\n        require(_pct_bps >= 10 * MathLib.THOUSAND); // the liquidation ratio has to be bigger or equal to 100%\n        Fileable(_spot).file(_ilk, \"mat\", MathLib.rdiv(_pct_bps, 10 * MathLib.THOUSAND));\n    }\n    /**\n        @dev Set minimum bid increase for collateral. Amount will be converted to the correct internal precision.\n        @dev Equation used for conversion is (1 + pct / 10,000) * WAD\n        @param _flip   The address of the ilk's flip core accounting contract\n        @param _pct_bps    The pct, in basis points, to set in integer form (x100). (ex. 5% = 5 * 100 = 500)\n    */\n    function setIlkMinAuctionBidIncrease(address _flip, uint256 _pct_bps) public {\n        require(_pct_bps < 10 * MathLib.THOUSAND);  // \"LibDssExec/incorrect-ilk-chop-precision\"\n        Fileable(_flip).file(\"beg\", MathLib.add(MathLib.WAD, MathLib.wdiv(_pct_bps, 10 * MathLib.THOUSAND)));\n    }\n    /**\n        @dev Set bid duration for a collateral type.\n        @param _flip   The address of the ilk's flip core accounting contract\n        @param _duration Amount of time for bids.\n    */\n    function setIlkBidDuration(address _flip, uint256 _duration) public {\n        Fileable(_flip).file(\"ttl\", _duration);\n    }\n    /**\n        @dev Set auction duration for a collateral type.\n        @param _flip   The address of the ilk's flip core accounting contract\n        @param _duration Amount of time for auctions.\n    */\n    function setIlkAuctionDuration(address _flip, uint256 _duration) public {\n        Fileable(_flip).file(\"tau\", _duration);\n    }\n    /**\n        @dev Set the stability fee for a given ilk.\n            Many of the settings that change weekly rely on the rate accumulator\n            described at https://docs.makerdao.com/smart-contract-modules/rates-module\n            To check this yourself, use the following rate calculation (example 8%):\n\n            $ bc -l <<< 'scale=27; e( l(1.08)/(60 * 60 * 24 * 365) )'\n\n            A table of rates can also be found at:\n            https://ipfs.io/ipfs/QmefQMseb3AiTapiAKKexdKHig8wroKuZbmLtPLv4u2YwW\n\n        @param _jug    The address of the Jug core accounting contract\n        @param _ilk    The ilk to update (ex. bytes32(\"ETH-A\") )\n        @param _rate   The accumulated rate (ex. 4% => 1000000001243680656318820312)\n        @param _doDrip `true` to accumulate stability fees for the collateral\n    */\n    function setIlkStabilityFee(address _jug, bytes32 _ilk, uint256 _rate, bool _doDrip) public {\n        require((_rate >= MathLib.RAY) && (_rate < 2 * MathLib.RAY));  // \"LibDssExec/ilk-stability-fee-out-of-bounds\"\n        if (_doDrip) Drippable(_jug).drip(_ilk);\n\n        Fileable(_jug).file(_ilk, \"duty\", _rate);\n    }\n\n    /***********************/\n    /*** Core Management ***/\n    /***********************/\n    /**\n        @dev Update collateral auction contracts.\n        @param _vat        Vat core contract address\n        @param _cat        Cat core contract address\n        @param _end        End core contract address\n        @param _flipperMom Flipper Mom core contract address\n        @param _ilk        The collateral's auction contract to update\n        @param _newFlip    New auction contract address\n        @param _oldFlip    Old auction contract address\n    */\n    function updateCollateralAuctionContract(\n        address _vat,\n        address _cat,\n        address _end,\n        address _flipperMom,\n        bytes32 _ilk,\n        address _newFlip,\n        address _oldFlip\n    ) public {\n        // Add new flip address to Cat\n        setContract(_cat, _ilk, \"flip\", _newFlip);\n\n        // Authorize MCD contracts from new flip\n        authorize(_newFlip, _cat);\n        authorize(_newFlip, _end);\n        authorize(_newFlip, _flipperMom);\n\n        // Authorize MCD contracts from old flip\n        deauthorize(_oldFlip, _cat);\n        deauthorize(_oldFlip, _end);\n        deauthorize(_oldFlip, _flipperMom);\n\n        // Transfer auction params from old flip to new flip\n        Fileable(_newFlip).file(\"beg\", AuctionLike(_oldFlip).beg());\n        Fileable(_newFlip).file(\"ttl\", AuctionLike(_oldFlip).ttl());\n        Fileable(_newFlip).file(\"tau\", AuctionLike(_oldFlip).tau());\n\n        // Sanity checks\n        require(AuctionLike(_newFlip).ilk() == _ilk);  // \"non-matching-ilk\"\n        require(AuctionLike(_newFlip).vat() == _vat);  // \"non-matching-vat\"\n    }\n    /**\n        @dev Update surplus auction contracts.\n        @param _vat     Vat core contract address\n        @param _vow     Vow core contract address\n        @param _newFlap New surplus auction contract address\n        @param _oldFlap Old surplus auction contract address\n    */\n    function updateSurplusAuctionContract(address _vat, address _vow, address _newFlap, address _oldFlap) public {\n\n        // Add new flap address to Vow\n        setContract(_vow, \"flapper\", _newFlap);\n\n        // Authorize MCD contracts from new flap\n        authorize(_newFlap, _vow);\n\n        // Authorize MCD contracts from old flap\n        deauthorize(_oldFlap, _vow);\n\n        // Transfer auction params from old flap to new flap\n        Fileable(_newFlap).file(\"beg\", AuctionLike(_oldFlap).beg());\n        Fileable(_newFlap).file(\"ttl\", AuctionLike(_oldFlap).ttl());\n        Fileable(_newFlap).file(\"tau\", AuctionLike(_oldFlap).tau());\n\n        // Sanity checks\n        require(AuctionLike(_newFlap).gem() == AuctionLike(_oldFlap).gem());  // \"non-matching-gem\"\n        require(AuctionLike(_newFlap).vat() == _vat);  // \"non-matching-vat\"\n    }\n    /**\n        @dev Update debt auction contracts.\n        @param _vat          Vat core contract address\n        @param _vow          Vow core contract address\n        @param _mkrAuthority MKRAuthority core contract address\n        @param _newFlop      New debt auction contract address\n        @param _oldFlop      Old debt auction contract address\n    */\n    function updateDebtAuctionContract(address _vat, address _vow, address _mkrAuthority, address _newFlop, address _oldFlop) public {\n        // Add new flop address to Vow\n        setContract(_vow, \"flopper\", _newFlop);\n\n        // Authorize MCD contracts for new flop\n        authorize(_newFlop, _vow);\n        authorize(_vat, _newFlop);\n        authorize(_mkrAuthority, _newFlop);\n\n        // Deauthorize MCD contracts for old flop\n        deauthorize(_oldFlop, _vow);\n        deauthorize(_vat, _oldFlop);\n        deauthorize(_mkrAuthority, _oldFlop);\n\n        // Transfer auction params from old flop to new flop\n        Fileable(_newFlop).file(\"beg\", AuctionLike(_oldFlop).beg());\n        Fileable(_newFlop).file(\"pad\", AuctionLike(_oldFlop).pad());\n        Fileable(_newFlop).file(\"ttl\", AuctionLike(_oldFlop).ttl());\n        Fileable(_newFlop).file(\"tau\", AuctionLike(_oldFlop).tau());\n\n        // Sanity checks\n        require(AuctionLike(_newFlop).gem() == AuctionLike(_oldFlop).gem()); // \"non-matching-gem\"\n        require(AuctionLike(_newFlop).vat() == _vat);  // \"non-matching-vat\"\n    }\n\n    /*************************/\n    /*** Oracle Management ***/\n    /*************************/\n    /**\n        @dev Adds oracle feeds to the Median's writer whitelist, allowing the feeds to write prices.\n        @param _median Median core contract address\n        @param _feeds      Array of oracle feed addresses to add to whitelist\n    */\n    function addWritersToMedianWhitelist(address _median, address[] memory _feeds) public {\n        OracleLike_2(_median).lift(_feeds);\n    }\n    /**\n        @dev Removes oracle feeds to the Median's writer whitelist, disallowing the feeds to write prices.\n        @param _median Median core contract address\n        @param _feeds      Array of oracle feed addresses to remove from whitelist\n    */\n    function removeWritersFromMedianWhitelist(address _median, address[] memory _feeds) public {\n        OracleLike_2(_median).drop(_feeds);\n    }\n    /**\n        @dev Adds addresses to the Median's reader whitelist, allowing the addresses to read prices from the median.\n        @param _median Median core contract address\n        @param _readers    Array of addresses to add to whitelist\n    */\n    function addReadersToMedianWhitelist(address _median, address[] memory _readers) public {\n        OracleLike_2(_median).kiss(_readers);\n    }\n    /**\n        @dev Adds an address to the Median's reader whitelist, allowing the address to read prices from the median.\n        @param _median Median core contract address\n        @param _reader     Address to add to whitelist\n    */\n    function addReaderToMedianWhitelist(address _median, address _reader) public {\n        OracleLike_2(_median).kiss(_reader);\n    }\n    /**\n        @dev Removes addresses from the Median's reader whitelist, disallowing the addresses to read prices from the median.\n        @param _median Median core contract address\n        @param _readers    Array of addresses to remove from whitelist\n    */\n    function removeReadersFromMedianWhitelist(address _median, address[] memory _readers) public {\n        OracleLike_2(_median).diss(_readers);\n    }\n    /**\n        @dev Removes an address to the Median's reader whitelist, disallowing the address to read prices from the median.\n        @param _median Median core contract address\n        @param _reader     Address to remove from whitelist\n    */\n    function removeReaderFromMedianWhitelist(address _median, address _reader) public {\n        OracleLike_2(_median).diss(_reader);\n    }\n    /**\n        @dev Sets the minimum number of valid messages from whitelisted oracle feeds needed to update median price.\n        @param _median Median core contract address\n        @param _minQuorum  Minimum number of valid messages from whitelisted oracle feeds needed to update median price (NOTE: MUST BE ODD NUMBER)\n    */\n    function setMedianWritersQuorum(address _median, uint256 _minQuorum) public {\n        OracleLike_2(_median).setBar(_minQuorum);\n    }\n    /**\n        @dev Adds an address to the Median's reader whitelist, allowing the address to read prices from the OSM.\n        @param _osm        Oracle Security Module (OSM) core contract address\n        @param _reader     Address to add to whitelist\n    */\n    function addReaderToOSMWhitelist(address _osm, address _reader) public {\n        OracleLike_2(_osm).kiss(_reader);\n    }\n    /**\n        @dev Removes an address to the Median's reader whitelist, disallowing the address to read prices from the OSM.\n        @param _osm        Oracle Security Module (OSM) core contract address\n        @param _reader     Address to remove from whitelist\n    */\n    function removeReaderFromOSMWhitelist(address _osm, address _reader) public {\n        OracleLike_2(_osm).diss(_reader);\n    }\n    /**\n        @dev Add OSM address to OSM mom, allowing it to be frozen by governance.\n        @param _osmMom     OSM Mom core contract address\n        @param _osm        Oracle Security Module (OSM) core contract address\n        @param _ilk        Collateral type using OSM\n    */\n    function allowOSMFreeze(address _osmMom, address _osm, bytes32 _ilk) public {\n        MomLike(_osmMom).setOsm(_ilk, _osm);\n    }\n\n\n    /*****************************/\n    /*** Collateral Onboarding ***/\n    /*****************************/\n\n    /**\n        @dev Performs basic functions and sanity checks to add a new collateral type to the MCD system\n        @param _vat      MCD_VAT\n        @param _cat      MCD_CAT\n        @param _jug      MCD_JUG\n        @param _end      MCD_END\n        @param _spot     MCD_SPOT\n        @param _reg      ILK_REGISTRY\n        @param _ilk      Collateral type key code [Ex. \"ETH-A\"]\n        @param _gem      Address of token contract\n        @param _join     Address of join adapter\n        @param _flip     Address of flipper\n        @param _pip      Address of price feed\n    */\n    function addCollateralBase(\n        address _vat,\n        address _cat,\n        address _jug,\n        address _end,\n        address _spot,\n        address _reg,\n        bytes32 _ilk,\n        address _gem,\n        address _join,\n        address _flip,\n        address _pip\n    ) public {\n        // Sanity checks\n        require(JoinLike_2(_join).vat() == _vat);     // \"join-vat-not-match\"\n        require(JoinLike_2(_join).ilk() == _ilk);     // \"join-ilk-not-match\"\n        require(JoinLike_2(_join).gem() == _gem);     // \"join-gem-not-match\"\n        require(JoinLike_2(_join).dec() ==\n                   ERC20(_gem).decimals());         // \"join-dec-not-match\"\n        require(AuctionLike(_flip).vat() == _vat);  // \"flip-vat-not-match\"\n        require(AuctionLike(_flip).cat() == _cat);  // \"flip-cat-not-match\"\n        require(AuctionLike(_flip).ilk() == _ilk);  // \"flip-ilk-not-match\"\n\n        // Set the token PIP in the Spotter\n        setContract(_spot, _ilk, \"pip\", _pip);\n\n        // Set the ilk Flipper in the Cat\n        setContract(_cat, _ilk, \"flip\", _flip);\n\n        // Init ilk in Vat & Jug\n        Initializable(_vat).init(_ilk);  // Vat\n        Initializable(_jug).init(_ilk);  // Jug\n\n        // Allow ilk Join to modify Vat registry\n        authorize(_vat, _join);\n\t\t// Allow the ilk Flipper to reduce the Cat litterbox on deal()\n        authorize(_cat, _flip);\n        // Allow Cat to kick auctions in ilk Flipper\n        authorize(_flip, _cat);\n        // Allow End to yank auctions in ilk Flipper\n        authorize(_flip, _end);\n\n        // Add new ilk to the IlkRegistry\n        RegistryLike_2(_reg).add(_join);\n    }\n}\n\n"
    }
  }
}
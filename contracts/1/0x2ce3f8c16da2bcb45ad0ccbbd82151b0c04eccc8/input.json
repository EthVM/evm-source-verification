{
  "language": "Solidity",
  "settings": {
    "libraries": {},
    "metadata": {
      "useLiteralContent": true
    },
    "optimizer": {
      "enabled": true,
      "runs": 200
    },
    "remappings": [],
    "outputSelection": {
      "*": {
        "*": [
          "*"
        ],
        "": [
          "*"
        ]
      }
    }
  },
  "sources": {
    "LightGeneralizedTCR.sol": {
      "content": "/**\r\n *  @authors: [@mtsalenc]\r\n *  @reviewers: [@clesaege]\r\n *  @auditors: []\r\n *  @bounties: []\r\n *  @deployments: []\r\n */\r\n\r\npragma solidity 0.5.17;\r\n\r\n\r\n/**\r\n * @title CappedMath\r\n * @dev Math operations with caps for under and overflow.\r\n */\r\nlibrary CappedMath {\r\n    uint constant private UINT_MAX = 2**256 - 1;\r\n\r\n    /**\r\n     * @dev Adds two unsigned integers, returns 2^256 - 1 on overflow.\r\n     */\r\n    function addCap(uint _a, uint _b) internal pure returns (uint) {\r\n        uint c = _a + _b;\r\n        return c >= _a ? c : UINT_MAX;\r\n    }\r\n\r\n    /**\r\n     * @dev Subtracts two integers, returns 0 on underflow.\r\n     */\r\n    function subCap(uint _a, uint _b) internal pure returns (uint) {\r\n        if (_b > _a)\r\n            return 0;\r\n        else\r\n            return _a - _b;\r\n    }\r\n\r\n    /**\r\n     * @dev Multiplies two unsigned integers, returns 2^256 - 1 on overflow.\r\n     */\r\n    function mulCap(uint _a, uint _b) internal pure returns (uint) {\r\n        // Gas optimization: this is cheaper than requiring '_a' not being zero, but the\r\n        // benefit is lost if '_b' is also tested.\r\n        // See: https://github.com/OpenZeppelin/openzeppelin-solidity/pull/522\r\n        if (_a == 0)\r\n            return 0;\r\n\r\n        uint c = _a * _b;\r\n        return c / _a == _b ? c : UINT_MAX;\r\n    }\r\n}\r\n\r\n\r\n/**\r\n *  @authors: [@hbarcelos]\r\n *  @reviewers: [@fnanni-0]\r\n *  @auditors: []\r\n *  @bounties: []\r\n *  @deployments: []\r\n */\r\n\r\n\r\n/**\r\n * @title CappedMath\r\n * @dev Math operations with caps for under and overflow.\r\n */\r\nlibrary CappedMath128 {\r\n    uint128 private constant UINT128_MAX = 2**128 - 1;\r\n\r\n    /**\r\n     * @dev Adds two unsigned integers, returns 2^128 - 1 on overflow.\r\n     */\r\n    function addCap(uint128 _a, uint128 _b) internal pure returns (uint128) {\r\n        uint128 c = _a + _b;\r\n        return c >= _a ? c : UINT128_MAX;\r\n    }\r\n\r\n    /**\r\n     * @dev Subtracts two integers, returns 0 on underflow.\r\n     */\r\n    function subCap(uint128 _a, uint128 _b) internal pure returns (uint128) {\r\n        if (_b > _a) return 0;\r\n        else return _a - _b;\r\n    }\r\n\r\n    /**\r\n     * @dev Multiplies two unsigned integers, returns 2^128 - 1 on overflow.\r\n     */\r\n    function mulCap(uint128 _a, uint128 _b) internal pure returns (uint128) {\r\n        if (_a == 0) return 0;\r\n\r\n        uint128 c = _a * _b;\r\n        return c / _a == _b ? c : UINT128_MAX;\r\n    }\r\n}\r\n\r\n \r\n /**\r\n *  @authors: [@ferittuncer]\r\n *  @reviewers: [@remedcu]\r\n *  @auditors: []\r\n *  @bounties: []\r\n *  @deployments: []\r\n */\r\n\r\n\r\n/** @title IArbitrable\r\n *  Arbitrable interface.\r\n *  When developing arbitrable contracts, we need to:\r\n *  -Define the action taken when a ruling is received by the contract.\r\n *  -Allow dispute creation. For this a function must call arbitrator.createDispute.value(_fee)(_choices,_extraData);\r\n */\r\ninterface IArbitrable {\r\n\r\n    /** @dev To be raised when a ruling is given.\r\n     *  @param _arbitrator The arbitrator giving the ruling.\r\n     *  @param _disputeID ID of the dispute in the Arbitrator contract.\r\n     *  @param _ruling The ruling which was given.\r\n     */\r\n    event Ruling(IArbitrator indexed _arbitrator, uint indexed _disputeID, uint _ruling);\r\n\r\n    /** @dev Give a ruling for a dispute. Must be called by the arbitrator.\r\n     *  The purpose of this function is to ensure that the address calling it has the right to rule on the contract.\r\n     *  @param _disputeID ID of the dispute in the Arbitrator contract.\r\n     *  @param _ruling Ruling given by the arbitrator. Note that 0 is reserved for \"Not able/wanting to make a decision\".\r\n     */\r\n    function rule(uint _disputeID, uint _ruling) external;\r\n}\r\n\r\n\r\n /**\r\n *  @authors: [@ferittuncer]\r\n *  @reviewers: [@remedcu]\r\n *  @auditors: []\r\n *  @bounties: []\r\n *  @deployments: []\r\n */\r\n\r\n\r\n/** @title Arbitrator\r\n *  Arbitrator abstract contract.\r\n *  When developing arbitrator contracts we need to:\r\n *  -Define the functions for dispute creation (createDispute) and appeal (appeal). Don't forget to store the arbitrated contract and the disputeID (which should be unique, may nbDisputes).\r\n *  -Define the functions for cost display (arbitrationCost and appealCost).\r\n *  -Allow giving rulings. For this a function must call arbitrable.rule(disputeID, ruling).\r\n */\r\ninterface IArbitrator {\r\n\r\n    enum DisputeStatus {Waiting, Appealable, Solved}\r\n\r\n    /** @dev To be emitted when a dispute is created.\r\n     *  @param _disputeID ID of the dispute.\r\n     *  @param _arbitrable The contract which created the dispute.\r\n     */\r\n    event DisputeCreation(uint indexed _disputeID, IArbitrable indexed _arbitrable);\r\n\r\n    /** @dev To be emitted when a dispute can be appealed.\r\n     *  @param _disputeID ID of the dispute.\r\n     *  @param _arbitrable The contract which created the dispute.\r\n     */\r\n    event AppealPossible(uint indexed _disputeID, IArbitrable indexed _arbitrable);\r\n\r\n    /** @dev To be emitted when the current ruling is appealed.\r\n     *  @param _disputeID ID of the dispute.\r\n     *  @param _arbitrable The contract which created the dispute.\r\n     */\r\n    event AppealDecision(uint indexed _disputeID, IArbitrable indexed _arbitrable);\r\n\r\n    /** @dev Create a dispute. Must be called by the arbitrable contract.\r\n     *  Must be paid at least arbitrationCost(_extraData).\r\n     *  @param _choices Amount of choices the arbitrator can make in this dispute.\r\n     *  @param _extraData Can be used to give additional info on the dispute to be created.\r\n     *  @return disputeID ID of the dispute created.\r\n     */\r\n    function createDispute(uint _choices, bytes calldata _extraData) external payable returns(uint disputeID);\r\n\r\n    /** @dev Compute the cost of arbitration. It is recommended not to increase it often, as it can be highly time and gas consuming for the arbitrated contracts to cope with fee augmentation.\r\n     *  @param _extraData Can be used to give additional info on the dispute to be created.\r\n     *  @return cost Amount to be paid.\r\n     */\r\n    function arbitrationCost(bytes calldata _extraData) external view returns(uint cost);\r\n\r\n    /** @dev Appeal a ruling. Note that it has to be called before the arbitrator contract calls rule.\r\n     *  @param _disputeID ID of the dispute to be appealed.\r\n     *  @param _extraData Can be used to give extra info on the appeal.\r\n     */\r\n    function appeal(uint _disputeID, bytes calldata _extraData) external payable;\r\n\r\n    /** @dev Compute the cost of appeal. It is recommended not to increase it often, as it can be higly time and gas consuming for the arbitrated contracts to cope with fee augmentation.\r\n     *  @param _disputeID ID of the dispute to be appealed.\r\n     *  @param _extraData Can be used to give additional info on the dispute to be created.\r\n     *  @return cost Amount to be paid.\r\n     */\r\n    function appealCost(uint _disputeID, bytes calldata _extraData) external view returns(uint cost);\r\n\r\n    /** @dev Compute the start and end of the dispute's current or next appeal period, if possible. If not known or appeal is impossible: should return (0, 0).\r\n     *  @param _disputeID ID of the dispute.\r\n     *  @return start The start of the period.\r\n     *  @return end The end of the period.\r\n     */\r\n    function appealPeriod(uint _disputeID) external view returns(uint start, uint end);\r\n\r\n    /** @dev Return the status of a dispute.\r\n     *  @param _disputeID ID of the dispute to rule.\r\n     *  @return status The status of the dispute.\r\n     */\r\n    function disputeStatus(uint _disputeID) external view returns(DisputeStatus status);\r\n\r\n    /** @dev Return the current ruling of a dispute. This is useful for parties to know if they should appeal.\r\n     *  @param _disputeID ID of the dispute.\r\n     *  @return ruling The ruling which has been given or the one which will be given if there is no appeal.\r\n     */\r\n    function currentRuling(uint _disputeID) external view returns(uint ruling);\r\n\r\n}\r\n\r\n\r\n/** @title IEvidence\r\n *  ERC-1497: Evidence Standard\r\n */\r\ninterface IEvidence {\r\n\r\n    /** @dev To be emitted when meta-evidence is submitted.\r\n     *  @param _metaEvidenceID Unique identifier of meta-evidence.\r\n     *  @param _evidence A link to the meta-evidence JSON.\r\n     */\r\n    event MetaEvidence(uint indexed _metaEvidenceID, string _evidence);\r\n\r\n    /** @dev To be raised when evidence is submitted. Should point to the resource (evidences are not to be stored on chain due to gas considerations).\r\n     *  @param _arbitrator The arbitrator of the contract.\r\n     *  @param _evidenceGroupID Unique identifier of the evidence group the evidence belongs to.\r\n     *  @param _party The address of the party submiting the evidence. Note that 0x0 refers to evidence not submitted by any party.\r\n     *  @param _evidence A URI to the evidence JSON file whose name should be its keccak256 hash followed by .json.\r\n     */\r\n    event Evidence(IArbitrator indexed _arbitrator, uint indexed _evidenceGroupID, address indexed _party, string _evidence);\r\n\r\n    /** @dev To be emitted when a dispute is created to link the correct meta-evidence to the disputeID.\r\n     *  @param _arbitrator The arbitrator of the contract.\r\n     *  @param _disputeID ID of the dispute in the Arbitrator contract.\r\n     *  @param _metaEvidenceID Unique identifier of meta-evidence.\r\n     *  @param _evidenceGroupID Unique identifier of the evidence group that is linked to this dispute.\r\n     */\r\n    event Dispute(IArbitrator indexed _arbitrator, uint indexed _disputeID, uint _metaEvidenceID, uint _evidenceGroupID);\r\n\r\n}\r\n\r\n\r\n/**\r\n *  @authors: [@unknownunknown1*, @mtsalenc*, @hbarcelos*]\r\n *  @reviewers: [@fnanni-0*, @greenlucid, @shalzz]\r\n *  @auditors: []\r\n *  @bounties: []\r\n *  @deployments: []\r\n */\r\n\r\n\r\n/**\r\n *  @title LightGeneralizedTCR\r\n *  This contract is a curated registry for any types of items. Just like a TCR contract it features the request-challenge protocol and appeal fees crowdfunding.\r\n *  The difference between LightGeneralizedTCR and GeneralizedTCR is that instead of storing item data in storage and event logs, LightCurate only stores the URI of item in the logs. This makes it considerably cheaper to use and allows more flexibility with the item columns.\r\n */\r\ncontract LightGeneralizedTCR is IArbitrable, IEvidence {\r\n    using CappedMath for uint256;\r\n    using CappedMath128 for uint128;\r\n\r\n    /* Enums */\r\n\r\n    enum Status {\r\n        Absent, // The item is not in the registry.\r\n        Registered, // The item is in the registry.\r\n        RegistrationRequested, // The item has a request to be added to the registry.\r\n        ClearingRequested // The item has a request to be removed from the registry.\r\n    }\r\n\r\n    enum Party {\r\n        None, // Party per default when there is no challenger or requester. Also used for unconclusive ruling.\r\n        Requester, // Party that made the request to change a status.\r\n        Challenger // Party that challenges the request to change a status.\r\n    }\r\n\r\n    enum RequestType {\r\n        Registration, // Identifies a request to register an item to the registry.\r\n        Clearing // Identifies a request to remove an item from the registry.\r\n    }\r\n\r\n    enum DisputeStatus {\r\n        None, // No dispute was created.\r\n        AwaitingRuling, // Dispute was created, but the final ruling was not given yet.\r\n        Resolved // Dispute was ruled.\r\n    }\r\n\r\n    /* Structs */\r\n\r\n    struct Item {\r\n        Status status; // The current status of the item.\r\n        uint128 sumDeposit; // The total deposit made by the requester and the challenger (if any).\r\n        uint120 requestCount; // The number of requests.\r\n        mapping(uint256 => Request) requests; // List of status change requests made for the item in the form requests[requestID].\r\n    }\r\n\r\n    // Arrays with 3 elements map with the Party enum for better readability:\r\n    // - 0: is unused, matches `Party.None`.\r\n    // - 1: for `Party.Requester`.\r\n    // - 2: for `Party.Challenger`.\r\n    struct Request {\r\n        RequestType requestType;\r\n        uint64 submissionTime; // Time when the request was made. Used to track when the challenge period ends.\r\n        uint24 arbitrationParamsIndex; // The index for the arbitration params for the request.\r\n        address payable requester; // Address of the requester.\r\n        // Pack the requester together with the other parameters, as they are written in the same request.\r\n        address payable challenger; // Address of the challenger, if any.\r\n    }\r\n\r\n    struct DisputeData {\r\n        uint256 disputeID; // The ID of the dispute on the arbitrator.\r\n        DisputeStatus status; // The current status of the dispute.\r\n        Party ruling; // The ruling given to a dispute. Only set after it has been resolved.\r\n        uint240 roundCount; // The number of rounds.\r\n        mapping(uint256 => Round) rounds; // Data of the different dispute rounds. rounds[roundId].\r\n    }\r\n\r\n    struct Round {\r\n        Party sideFunded; // Stores the side that successfully paid the appeal fees in the latest round. Note that if both sides have paid a new round is created.\r\n        uint256 feeRewards; // Sum of reimbursable fees and stake rewards available to the parties that made contributions to the side that ultimately wins a dispute.\r\n        uint256[3] amountPaid; // Tracks the sum paid for each Party in this round.\r\n        mapping(address => uint256[3]) contributions; // Maps contributors to their contributions for each side in the form contributions[address][party].\r\n    }\r\n\r\n    struct ArbitrationParams {\r\n        IArbitrator arbitrator; // The arbitrator trusted to solve disputes for this request.\r\n        bytes arbitratorExtraData; // The extra data for the trusted arbitrator of this request.\r\n    }\r\n\r\n    /* Constants */\r\n\r\n    uint256 public constant RULING_OPTIONS = 2; // The amount of non 0 choices the arbitrator can give.\r\n    uint256 private constant RESERVED_ROUND_ID = 0; // For compatibility with GeneralizedTCR consider the request/challenge cycle the first round (index 0).\r\n\r\n    /* Storage */\r\n\r\n    bool private initialized;\r\n\r\n    address public relayerContract; // The contract that is used to add or remove items directly to speed up the interchain communication.\r\n    address public governor; // The address that can make changes to the parameters of the contract.\r\n\r\n    uint256 public submissionBaseDeposit; // The base deposit to submit an item.\r\n    uint256 public removalBaseDeposit; // The base deposit to remove an item.\r\n    uint256 public submissionChallengeBaseDeposit; // The base deposit to challenge a submission.\r\n    uint256 public removalChallengeBaseDeposit; // The base deposit to challenge a removal request.\r\n    uint256 public challengePeriodDuration; // The time after which a request becomes executable if not challenged.\r\n\r\n    // Multipliers are in basis points.\r\n    uint256 public winnerStakeMultiplier; // Multiplier for calculating the fee stake paid by the party that won the previous round.\r\n    uint256 public loserStakeMultiplier; // Multiplier for calculating the fee stake paid by the party that lost the previous round.\r\n    uint256 public sharedStakeMultiplier; // Multiplier for calculating the fee stake that must be paid in the case where arbitrator refused to arbitrate.\r\n    uint256 public constant MULTIPLIER_DIVISOR = 10000; // Divisor parameter for multipliers.\r\n\r\n    mapping(bytes32 => Item) public items; // Maps the item ID to its data in the form items[_itemID].\r\n    mapping(address => mapping(uint256 => bytes32)) public arbitratorDisputeIDToItemID; // Maps a dispute ID to the ID of the item with the disputed request in the form arbitratorDisputeIDToItemID[arbitrator][disputeID].\r\n    mapping(bytes32 => mapping(uint256 => DisputeData)) public requestsDisputeData; // Maps an item and a request to the data of the dispute related to them. requestsDisputeData[itemID][requestIndex]\r\n    ArbitrationParams[] public arbitrationParamsChanges;\r\n\r\n    /* Modifiers */\r\n\r\n    modifier onlyGovernor() {\r\n        require(msg.sender == governor, \"The caller must be the governor.\");\r\n        _;\r\n    }\r\n\r\n    modifier onlyRelayer() {\r\n        require(msg.sender == relayerContract, \"The caller must be the relay.\");\r\n        _;\r\n    }\r\n\r\n    /* Events */\r\n\r\n    /**\r\n     * @dev Emitted when a party makes a request, raises a dispute or when a request is resolved.\r\n     * @param _itemID The ID of the affected item.\r\n     * @param _updatedDirectly Whether this was emitted in either `addItemDirectly` or `removeItemDirectly`. This is used in the subgraph.\r\n     */\r\n    event ItemStatusChange(bytes32 indexed _itemID, bool _updatedDirectly);\r\n\r\n    /**\r\n     * @dev Emitted when someone submits an item for the first time.\r\n     * @param _itemID The ID of the new item.\r\n     * @param _data The item data URI.\r\n     * @param _addedDirectly Whether the item was added via `addItemDirectly`.\r\n     */\r\n    event NewItem(bytes32 indexed _itemID, string _data, bool _addedDirectly);\r\n\r\n    /**\r\n     * @dev Emitted when someone submits a request.\r\n     * @param _itemID The ID of the affected item.\r\n     * @param _evidenceGroupID Unique identifier of the evidence group the evidence belongs to.\r\n     */\r\n    event RequestSubmitted(bytes32 indexed _itemID, uint256 _evidenceGroupID);\r\n\r\n    /**\r\n     * @dev Emitted when a party contributes to an appeal. The roundID assumes the initial request and challenge deposits are the first round. This is done so indexers can know more information about the contribution without using call handlers.\r\n     * @param _itemID The ID of the item.\r\n     * @param _requestID The index of the request that received the contribution.\r\n     * @param _roundID The index of the round that received the contribution.\r\n     * @param _contributor The address making the contribution.\r\n     * @param _contribution How much of the contribution was accepted.\r\n     * @param _side The party receiving the contribution.\r\n     */\r\n    event Contribution(\r\n        bytes32 indexed _itemID,\r\n        uint256 _requestID,\r\n        uint256 _roundID,\r\n        address indexed _contributor,\r\n        uint256 _contribution,\r\n        Party _side\r\n    );\r\n\r\n    /**\r\n     * @dev Emitted when the address of the connected TCR is set. The connected TCR is an instance of the Generalized TCR contract where each item is the address of a TCR related to this one.\r\n     * @param _connectedTCR The address of the connected TCR.\r\n     */\r\n    event ConnectedTCRSet(address indexed _connectedTCR);\r\n\r\n    /**\r\n     * @dev Emitted when someone withdraws more than 0 rewards.\r\n     * @param _beneficiary The address that made contributions to a request.\r\n     * @param _itemID The ID of the item submission to withdraw from.\r\n     * @param _request The request from which to withdraw.\r\n     * @param _round The round from which to withdraw.\r\n     * @param _reward The amount withdrawn.\r\n     */\r\n    event RewardWithdrawn(\r\n        address indexed _beneficiary,\r\n        bytes32 indexed _itemID,\r\n        uint256 _request,\r\n        uint256 _round,\r\n        uint256 _reward\r\n    );\r\n\r\n    /**\r\n     * @dev Initialize the arbitrable curated registry.\r\n     * @param _arbitrator Arbitrator to resolve potential disputes. The arbitrator is trusted to support appeal periods and not reenter.\r\n     * @param _arbitratorExtraData Extra data for the trusted arbitrator contract.\r\n     * @param _connectedTCR The address of the TCR that stores related TCR addresses. This parameter can be left empty.\r\n     * @param _registrationMetaEvidence The URI of the meta evidence object for registration requests.\r\n     * @param _clearingMetaEvidence The URI of the meta evidence object for clearing requests.\r\n     * @param _governor The trusted governor of this contract.\r\n     * @param _baseDeposits The base deposits for requests/challenges as follows:\r\n     * - The base deposit to submit an item.\r\n     * - The base deposit to remove an item.\r\n     * - The base deposit to challenge a submission.\r\n     * - The base deposit to challenge a removal request.\r\n     * @param _challengePeriodDuration The time in seconds parties have to challenge a request.\r\n     * @param _stakeMultipliers Multipliers of the arbitration cost in basis points (see MULTIPLIER_DIVISOR) as follows:\r\n     * - The multiplier applied to each party's fee stake for a round when there is no winner/loser in the previous round (e.g. when the arbitrator refused to arbitrate).\r\n     * - The multiplier applied to the winner's fee stake for the subsequent round.\r\n     * - The multiplier applied to the loser's fee stake for the subsequent round.\r\n     * @param _relayerContract The address of the relay contract to add/remove items directly.\r\n     */\r\n    function initialize(\r\n        IArbitrator _arbitrator,\r\n        bytes calldata _arbitratorExtraData,\r\n        address _connectedTCR,\r\n        string calldata _registrationMetaEvidence,\r\n        string calldata _clearingMetaEvidence,\r\n        address _governor,\r\n        uint256[4] calldata _baseDeposits,\r\n        uint256 _challengePeriodDuration,\r\n        uint256[3] calldata _stakeMultipliers,\r\n        address _relayerContract\r\n    ) external {\r\n        require(!initialized, \"Already initialized.\");\r\n\r\n        emit ConnectedTCRSet(_connectedTCR);\r\n\r\n        governor = _governor;\r\n        submissionBaseDeposit = _baseDeposits[0];\r\n        removalBaseDeposit = _baseDeposits[1];\r\n        submissionChallengeBaseDeposit = _baseDeposits[2];\r\n        removalChallengeBaseDeposit = _baseDeposits[3];\r\n        challengePeriodDuration = _challengePeriodDuration;\r\n        sharedStakeMultiplier = _stakeMultipliers[0];\r\n        winnerStakeMultiplier = _stakeMultipliers[1];\r\n        loserStakeMultiplier = _stakeMultipliers[2];\r\n        relayerContract = _relayerContract;\r\n\r\n        _doChangeArbitrationParams(_arbitrator, _arbitratorExtraData, _registrationMetaEvidence, _clearingMetaEvidence);\r\n\r\n        initialized = true;\r\n    }\r\n\r\n    /* External and Public */\r\n\r\n    // ************************ //\r\n    // *       Requests       * //\r\n    // ************************ //\r\n\r\n    /**\r\n     * @dev Directly add an item to the list bypassing request-challenge. Can only be used by the relay contract.\r\n     * @param _item The URI to the item data.\r\n     */\r\n    function addItemDirectly(string calldata _item) external onlyRelayer {\r\n        bytes32 itemID = keccak256(abi.encodePacked(_item));\r\n        Item storage item = items[itemID];\r\n        require(item.status == Status.Absent, \"Item must be absent to be added.\");\r\n\r\n        // Note that if the item is added directly once, the next time it is added it will emit this event again.\r\n        if (item.requestCount == 0) {\r\n            emit NewItem(itemID, _item, true);\r\n        }\r\n\r\n        item.status = Status.Registered;\r\n\r\n        emit ItemStatusChange(itemID, true);\r\n    }\r\n\r\n    /**\r\n     * @dev Directly remove an item from the list bypassing request-challenge. Can only be used by the relay contract.\r\n     * @param _itemID The ID of the item to remove.\r\n     */\r\n    function removeItemDirectly(bytes32 _itemID) external onlyRelayer {\r\n        Item storage item = items[_itemID];\r\n        require(item.status == Status.Registered, \"Item must be registered to be removed.\");\r\n\r\n        item.status = Status.Absent;\r\n\r\n        emit ItemStatusChange(_itemID, true);\r\n    }\r\n\r\n    /**\r\n     * @dev Submit a request to register an item. Accepts enough ETH to cover the deposit, reimburses the rest.\r\n     * @param _item The URI to the item data.\r\n     */\r\n    function addItem(string calldata _item) external payable {\r\n        bytes32 itemID = keccak256(abi.encodePacked(_item));\r\n        Item storage item = items[itemID];\r\n\r\n        // Extremely unlikely, but we check that for correctness sake.\r\n        require(item.requestCount < uint120(-1), \"Too many requests for item.\");\r\n        require(item.status == Status.Absent, \"Item must be absent to be added.\");\r\n\r\n        // Note that if the item was added previously using `addItemDirectly`, the event will be emitted again here.\r\n        if (item.requestCount == 0) {\r\n            emit NewItem(itemID, _item, false);\r\n        }\r\n\r\n        Request storage request = item.requests[item.requestCount++];\r\n        uint256 arbitrationParamsIndex = arbitrationParamsChanges.length - 1;\r\n        IArbitrator arbitrator = arbitrationParamsChanges[arbitrationParamsIndex].arbitrator;\r\n        bytes storage arbitratorExtraData = arbitrationParamsChanges[arbitrationParamsIndex].arbitratorExtraData;\r\n\r\n        uint256 arbitrationCost = arbitrator.arbitrationCost(arbitratorExtraData);\r\n        uint256 totalCost = arbitrationCost.addCap(submissionBaseDeposit);\r\n        require(msg.value >= totalCost, \"You must fully fund the request.\");\r\n\r\n        // Casting is safe here because this line will never be executed in case\r\n        // totalCost > type(uint128).max, since it would be an unpayable value.\r\n        item.sumDeposit = uint128(totalCost);\r\n        item.status = Status.RegistrationRequested;\r\n\r\n        request.requestType = RequestType.Registration;\r\n        request.submissionTime = uint64(block.timestamp);\r\n        request.arbitrationParamsIndex = uint24(arbitrationParamsIndex);\r\n        request.requester = msg.sender;\r\n\r\n        emit RequestSubmitted(itemID, getEvidenceGroupID(itemID, item.requestCount - 1));\r\n\r\n        emit Contribution(itemID, item.requestCount - 1, RESERVED_ROUND_ID, msg.sender, totalCost, Party.Requester);\r\n\r\n        if (msg.value > totalCost) {\r\n            msg.sender.send(msg.value - totalCost);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * @dev Submit a request to remove an item from the list. Accepts enough ETH to cover the deposit, reimburses the rest.\r\n     * @param _itemID The ID of the item to remove.\r\n     * @param _evidence A link to an evidence using its URI. Ignored if not provided.\r\n     */\r\n    function removeItem(bytes32 _itemID, string calldata _evidence) external payable {\r\n        Item storage item = items[_itemID];\r\n\r\n        // Extremely unlikely, but we check that for correctness sake.\r\n        require(item.requestCount < uint120(-1), \"Too many requests for item.\");\r\n        require(item.status == Status.Registered, \"Item must be registered to be removed.\");\r\n\r\n        Request storage request = item.requests[item.requestCount++];\r\n        uint256 arbitrationParamsIndex = arbitrationParamsChanges.length - 1;\r\n        IArbitrator arbitrator = arbitrationParamsChanges[arbitrationParamsIndex].arbitrator;\r\n        bytes storage arbitratorExtraData = arbitrationParamsChanges[arbitrationParamsIndex].arbitratorExtraData;\r\n\r\n        uint256 arbitrationCost = arbitrator.arbitrationCost(arbitratorExtraData);\r\n        uint256 totalCost = arbitrationCost.addCap(removalBaseDeposit);\r\n        require(msg.value >= totalCost, \"You must fully fund the request.\");\r\n\r\n        // Casting is safe here because this line will never be executed in case\r\n        // totalCost > type(uint128).max, since it would be an unpayable value.\r\n        item.sumDeposit = uint128(totalCost);\r\n        item.status = Status.ClearingRequested;\r\n\r\n        request.submissionTime = uint64(block.timestamp);\r\n        request.arbitrationParamsIndex = uint24(arbitrationParamsIndex);\r\n        request.requester = msg.sender;\r\n        request.requestType = RequestType.Clearing;\r\n\r\n        uint256 evidenceGroupID = getEvidenceGroupID(_itemID, item.requestCount - 1);\r\n\r\n        emit RequestSubmitted(_itemID, evidenceGroupID);\r\n\r\n        emit Contribution(_itemID, item.requestCount - 1, RESERVED_ROUND_ID, msg.sender, totalCost, Party.Requester);\r\n\r\n        // Emit evidence if it was provided.\r\n        if (bytes(_evidence).length > 0) {\r\n            emit Evidence(arbitrator, evidenceGroupID, msg.sender, _evidence);\r\n        }\r\n\r\n        if (msg.value > totalCost) {\r\n            msg.sender.send(msg.value - totalCost);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * @dev Challenges the request of the item. Accepts enough ETH to cover the deposit, reimburses the rest.\r\n     * @param _itemID The ID of the item which request to challenge.\r\n     * @param _evidence A link to an evidence using its URI. Ignored if not provided.\r\n     */\r\n    function challengeRequest(bytes32 _itemID, string calldata _evidence) external payable {\r\n        Item storage item = items[_itemID];\r\n        require(item.status > Status.Registered, \"The item must have a pending request.\");\r\n\r\n        uint256 lastRequestIndex = item.requestCount - 1;\r\n        Request storage request = item.requests[lastRequestIndex];\r\n        require(\r\n            block.timestamp - request.submissionTime <= challengePeriodDuration,\r\n            \"Challenges must occur during the challenge period.\"\r\n        );\r\n\r\n        DisputeData storage disputeData = requestsDisputeData[_itemID][lastRequestIndex];\r\n        require(disputeData.status == DisputeStatus.None, \"The request should not have already been disputed.\");\r\n\r\n        ArbitrationParams storage arbitrationParams = arbitrationParamsChanges[request.arbitrationParamsIndex];\r\n        IArbitrator arbitrator = arbitrationParams.arbitrator;\r\n\r\n        uint256 arbitrationCost = arbitrator.arbitrationCost(arbitrationParams.arbitratorExtraData);\r\n        uint256 totalCost;\r\n        {\r\n            uint256 challengerBaseDeposit = item.status == Status.RegistrationRequested\r\n                ? submissionChallengeBaseDeposit\r\n                : removalChallengeBaseDeposit;\r\n            totalCost = arbitrationCost.addCap(challengerBaseDeposit);\r\n        }\r\n        require(msg.value >= totalCost, \"You must fully fund the challenge.\");\r\n\r\n        emit Contribution(_itemID, lastRequestIndex, RESERVED_ROUND_ID, msg.sender, totalCost, Party.Challenger);\r\n\r\n        // Casting is safe here because this line will never be executed in case\r\n        // totalCost > type(uint128).max, since it would be an unpayable value.\r\n        item.sumDeposit = item.sumDeposit.addCap(uint128(totalCost)).subCap(uint128(arbitrationCost));\r\n\r\n        request.challenger = msg.sender;\r\n\r\n        // Raise a dispute.\r\n        disputeData.disputeID = arbitrator.createDispute.value(arbitrationCost)(\r\n            RULING_OPTIONS,\r\n            arbitrationParams.arbitratorExtraData\r\n        );\r\n        disputeData.status = DisputeStatus.AwaitingRuling;\r\n        // For compatibility with GeneralizedTCR consider the request/challenge cycle\r\n        // the first round (index 0), so we need to make the next round index 1.\r\n        disputeData.roundCount = 2;\r\n\r\n        arbitratorDisputeIDToItemID[address(arbitrator)][disputeData.disputeID] = _itemID;\r\n\r\n        uint256 metaEvidenceID = 2 * request.arbitrationParamsIndex + uint256(request.requestType);\r\n        uint256 evidenceGroupID = getEvidenceGroupID(_itemID, lastRequestIndex);\r\n        emit Dispute(arbitrator, disputeData.disputeID, metaEvidenceID, evidenceGroupID);\r\n\r\n        if (bytes(_evidence).length > 0) {\r\n            emit Evidence(arbitrator, evidenceGroupID, msg.sender, _evidence);\r\n        }\r\n\r\n        if (msg.value > totalCost) {\r\n            msg.sender.send(msg.value - totalCost);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * @dev Takes up to the total amount required to fund a side of an appeal. Reimburses the rest. Creates an appeal if both sides are fully funded.\r\n     * @param _itemID The ID of the item which request to fund.\r\n     * @param _side The recipient of the contribution.\r\n     */\r\n    function fundAppeal(bytes32 _itemID, Party _side) external payable {\r\n        require(_side > Party.None, \"Invalid side.\");\r\n\r\n        Item storage item = items[_itemID];\r\n        require(item.status > Status.Registered, \"The item must have a pending request.\");\r\n\r\n        uint256 lastRequestIndex = item.requestCount - 1;\r\n        Request storage request = item.requests[lastRequestIndex];\r\n\r\n        DisputeData storage disputeData = requestsDisputeData[_itemID][lastRequestIndex];\r\n        require(\r\n            disputeData.status == DisputeStatus.AwaitingRuling,\r\n            \"A dispute must have been raised to fund an appeal.\"\r\n        );\r\n\r\n        ArbitrationParams storage arbitrationParams = arbitrationParamsChanges[request.arbitrationParamsIndex];\r\n        IArbitrator arbitrator = arbitrationParams.arbitrator;\r\n\r\n        uint256 lastRoundIndex = disputeData.roundCount - 1;\r\n        Round storage round = disputeData.rounds[lastRoundIndex];\r\n        require(round.sideFunded != _side, \"Side already fully funded.\");\r\n\r\n        uint256 multiplier;\r\n        {\r\n            (uint256 appealPeriodStart, uint256 appealPeriodEnd) = arbitrator.appealPeriod(disputeData.disputeID);\r\n            require(\r\n                block.timestamp >= appealPeriodStart && block.timestamp < appealPeriodEnd,\r\n                \"Contributions must be made within the appeal period.\"\r\n            );\r\n\r\n            Party winner = Party(arbitrator.currentRuling(disputeData.disputeID));\r\n            if (winner == Party.None) {\r\n                multiplier = sharedStakeMultiplier;\r\n            } else if (_side == winner) {\r\n                multiplier = winnerStakeMultiplier;\r\n            } else {\r\n                multiplier = loserStakeMultiplier;\r\n                require(\r\n                    block.timestamp < (appealPeriodStart + appealPeriodEnd) / 2,\r\n                    \"The loser must contribute during the first half of the appeal period.\"\r\n                );\r\n            }\r\n        }\r\n\r\n        uint256 appealCost = arbitrator.appealCost(disputeData.disputeID, arbitrationParams.arbitratorExtraData);\r\n        uint256 totalCost = appealCost.addCap(appealCost.mulCap(multiplier) / MULTIPLIER_DIVISOR);\r\n        contribute(_itemID, lastRequestIndex, lastRoundIndex, uint256(_side), msg.sender, msg.value, totalCost);\r\n\r\n        if (round.amountPaid[uint256(_side)] >= totalCost) {\r\n            if (round.sideFunded == Party.None) {\r\n                round.sideFunded = _side;\r\n            } else {\r\n                // Resets the value because both sides are funded.\r\n                round.sideFunded = Party.None;\r\n\r\n                // Raise appeal if both sides are fully funded.\r\n                arbitrator.appeal.value(appealCost)(disputeData.disputeID, arbitrationParams.arbitratorExtraData);\r\n                disputeData.roundCount++;\r\n                round.feeRewards = round.feeRewards.subCap(appealCost);\r\n            }\r\n        }\r\n    }\r\n\r\n    /**\r\n     * @dev If a dispute was raised, sends the fee stake rewards and reimbursements proportionally to the contributions made to the winner of a dispute.\r\n     * @param _beneficiary The address that made contributions to a request.\r\n     * @param _itemID The ID of the item submission to withdraw from.\r\n     * @param _requestID The request from which to withdraw from.\r\n     * @param _roundID The round from which to withdraw from.\r\n     */\r\n    function withdrawFeesAndRewards(\r\n        address payable _beneficiary,\r\n        bytes32 _itemID,\r\n        uint256 _requestID,\r\n        uint256 _roundID\r\n    ) external {\r\n        DisputeData storage disputeData = requestsDisputeData[_itemID][_requestID];\r\n\r\n        require(disputeData.status == DisputeStatus.Resolved, \"Request must be resolved.\");\r\n\r\n        Round storage round = disputeData.rounds[_roundID];\r\n\r\n        uint256 reward;\r\n        if (_roundID == disputeData.roundCount - 1) {\r\n            // Reimburse if not enough fees were raised to appeal the ruling.\r\n            reward =\r\n                round.contributions[_beneficiary][uint256(Party.Requester)] +\r\n                round.contributions[_beneficiary][uint256(Party.Challenger)];\r\n        } else if (disputeData.ruling == Party.None) {\r\n            uint256 totalFeesInRound = round.amountPaid[uint256(Party.Challenger)] +\r\n                round.amountPaid[uint256(Party.Requester)];\r\n            uint256 claimableFees = round.contributions[_beneficiary][uint256(Party.Challenger)] +\r\n                round.contributions[_beneficiary][uint256(Party.Requester)];\r\n            reward = totalFeesInRound > 0 ? (claimableFees * round.feeRewards) / totalFeesInRound : 0;\r\n        } else {\r\n            // Reward the winner.\r\n            reward = round.amountPaid[uint256(disputeData.ruling)] > 0\r\n                ? (round.contributions[_beneficiary][uint256(disputeData.ruling)] * round.feeRewards) /\r\n                    round.amountPaid[uint256(disputeData.ruling)]\r\n                : 0;\r\n        }\r\n        round.contributions[_beneficiary][uint256(Party.Requester)] = 0;\r\n        round.contributions[_beneficiary][uint256(Party.Challenger)] = 0;\r\n\r\n        if (reward > 0) {\r\n            _beneficiary.send(reward);\r\n            emit RewardWithdrawn(_beneficiary, _itemID, _requestID, _roundID, reward);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * @dev Executes an unchallenged request if the challenge period has passed.\r\n     * @param _itemID The ID of the item to execute.\r\n     */\r\n    function executeRequest(bytes32 _itemID) external {\r\n        Item storage item = items[_itemID];\r\n        uint256 lastRequestIndex = items[_itemID].requestCount - 1;\r\n\r\n        Request storage request = item.requests[lastRequestIndex];\r\n        require(\r\n            block.timestamp - request.submissionTime > challengePeriodDuration,\r\n            \"Time to challenge the request must pass.\"\r\n        );\r\n\r\n        DisputeData storage disputeData = requestsDisputeData[_itemID][lastRequestIndex];\r\n        require(disputeData.status == DisputeStatus.None, \"The request should not be disputed.\");\r\n\r\n        if (item.status == Status.RegistrationRequested) {\r\n            item.status = Status.Registered;\r\n        } else if (item.status == Status.ClearingRequested) {\r\n            item.status = Status.Absent;\r\n        } else {\r\n            revert(\"There must be a request.\");\r\n        }\r\n\r\n        emit ItemStatusChange(_itemID, false);\r\n\r\n        uint256 sumDeposit = item.sumDeposit;\r\n        item.sumDeposit = 0;\r\n\r\n        if (sumDeposit > 0) {\r\n            // reimburse the requester\r\n            request.requester.send(sumDeposit);\r\n        }\r\n    }\r\n\r\n    /**\r\n     * @dev Give a ruling for a dispute. Can only be called by the arbitrator. TRUSTED.\r\n     * Accounts for the situation where the winner loses a case due to paying less appeal fees than expected.\r\n     * @param _disputeID ID of the dispute in the arbitrator contract.\r\n     * @param _ruling Ruling given by the arbitrator. Note that 0 is reserved for \"Refused to arbitrate\".\r\n     */\r\n    function rule(uint256 _disputeID, uint256 _ruling) external {\r\n        require(_ruling <= RULING_OPTIONS, \"Invalid ruling option\");\r\n\r\n        bytes32 itemID = arbitratorDisputeIDToItemID[msg.sender][_disputeID];\r\n        Item storage item = items[itemID];\r\n        uint256 lastRequestIndex = items[itemID].requestCount - 1;\r\n        Request storage request = item.requests[lastRequestIndex];\r\n\r\n        DisputeData storage disputeData = requestsDisputeData[itemID][lastRequestIndex];\r\n        require(disputeData.status == DisputeStatus.AwaitingRuling, \"The request must not be resolved.\");\r\n\r\n        ArbitrationParams storage arbitrationParams = arbitrationParamsChanges[request.arbitrationParamsIndex];\r\n        require(address(arbitrationParams.arbitrator) == msg.sender, \"Only the arbitrator can give a ruling\");\r\n\r\n        uint256 finalRuling;\r\n        Round storage round = disputeData.rounds[disputeData.roundCount - 1];\r\n\r\n        // If one side paid its fees, the ruling is in its favor.\r\n        // Note that if the other side had also paid, sideFudned would have been reset\r\n        // and an appeal would have been created.\r\n        if (round.sideFunded == Party.Requester) {\r\n            finalRuling = uint256(Party.Requester);\r\n        } else if (round.sideFunded == Party.Challenger) {\r\n            finalRuling = uint256(Party.Challenger);\r\n        } else {\r\n            finalRuling = _ruling;\r\n        }\r\n\r\n        emit Ruling(IArbitrator(msg.sender), _disputeID, finalRuling);\r\n\r\n        Party winner = Party(finalRuling);\r\n\r\n        disputeData.status = DisputeStatus.Resolved;\r\n        disputeData.ruling = winner;\r\n\r\n        uint256 sumDeposit = item.sumDeposit;\r\n        item.sumDeposit = 0;\r\n\r\n        if (winner == Party.None) {\r\n            // If the arbitrator refuse to rule, then the item status should be the same it was before the request.\r\n            // Regarding item.status this is equivalent to the challenger winning the dispute.\r\n            item.status = item.status == Status.RegistrationRequested ? Status.Absent : Status.Registered;\r\n\r\n            // Since nobody has won, then we reimburse both parties equally.\r\n            // If item.sumDeposit is odd, 1 wei will remain in the contract balance.\r\n            uint256 halfSumDeposit = sumDeposit / 2;\r\n\r\n            request.requester.send(halfSumDeposit);\r\n            request.challenger.send(halfSumDeposit);\r\n        } else if (winner == Party.Requester) {\r\n            item.status = item.status == Status.RegistrationRequested ? Status.Registered : Status.Absent;\r\n\r\n            request.requester.send(sumDeposit);\r\n        } else {\r\n            item.status = item.status == Status.RegistrationRequested ? Status.Absent : Status.Registered;\r\n\r\n            request.challenger.send(sumDeposit);\r\n        }\r\n\r\n        emit ItemStatusChange(itemID, false);\r\n    }\r\n\r\n    /**\r\n     * @dev Submit a reference to evidence. EVENT.\r\n     * @param _itemID The ID of the item which the evidence is related to.\r\n     * @param _evidence A link to an evidence using its URI.\r\n     */\r\n    function submitEvidence(bytes32 _itemID, string calldata _evidence) external {\r\n        Item storage item = items[_itemID];\r\n        uint256 lastRequestIndex = item.requestCount - 1;\r\n\r\n        Request storage request = item.requests[lastRequestIndex];\r\n        ArbitrationParams storage arbitrationParams = arbitrationParamsChanges[request.arbitrationParamsIndex];\r\n\r\n        emit Evidence(\r\n            arbitrationParams.arbitrator,\r\n            getEvidenceGroupID(_itemID, lastRequestIndex),\r\n            msg.sender,\r\n            _evidence\r\n        );\r\n    }\r\n\r\n    // ************************ //\r\n    // *      Governance      * //\r\n    // ************************ //\r\n\r\n    /**\r\n     * @dev Change the duration of the challenge period.\r\n     * @param _challengePeriodDuration The new duration of the challenge period.\r\n     */\r\n    function changeChallengePeriodDuration(uint256 _challengePeriodDuration) external onlyGovernor {\r\n        challengePeriodDuration = _challengePeriodDuration;\r\n    }\r\n\r\n    /**\r\n     * @dev Change the base amount required as a deposit to submit an item.\r\n     * @param _submissionBaseDeposit The new base amount of wei required to submit an item.\r\n     */\r\n    function changeSubmissionBaseDeposit(uint256 _submissionBaseDeposit) external onlyGovernor {\r\n        submissionBaseDeposit = _submissionBaseDeposit;\r\n    }\r\n\r\n    /**\r\n     * @dev Change the base amount required as a deposit to remove an item.\r\n     * @param _removalBaseDeposit The new base amount of wei required to remove an item.\r\n     */\r\n    function changeRemovalBaseDeposit(uint256 _removalBaseDeposit) external onlyGovernor {\r\n        removalBaseDeposit = _removalBaseDeposit;\r\n    }\r\n\r\n    /**\r\n     * @dev Change the base amount required as a deposit to challenge a submission.\r\n     * @param _submissionChallengeBaseDeposit The new base amount of wei required to challenge a submission.\r\n     */\r\n    function changeSubmissionChallengeBaseDeposit(uint256 _submissionChallengeBaseDeposit) external onlyGovernor {\r\n        submissionChallengeBaseDeposit = _submissionChallengeBaseDeposit;\r\n    }\r\n\r\n    /**\r\n     * @dev Change the base amount required as a deposit to challenge a removal request.\r\n     * @param _removalChallengeBaseDeposit The new base amount of wei required to challenge a removal request.\r\n     */\r\n    function changeRemovalChallengeBaseDeposit(uint256 _removalChallengeBaseDeposit) external onlyGovernor {\r\n        removalChallengeBaseDeposit = _removalChallengeBaseDeposit;\r\n    }\r\n\r\n    /**\r\n     * @dev Change the governor of the curated registry.\r\n     * @param _governor The address of the new governor.\r\n     */\r\n    function changeGovernor(address _governor) external onlyGovernor {\r\n        governor = _governor;\r\n    }\r\n\r\n    /**\r\n     * @dev Change the proportion of arbitration fees that must be paid as fee stake by parties when there is no winner or loser.\r\n     * @param _sharedStakeMultiplier Multiplier of arbitration fees that must be paid as fee stake. In basis points.\r\n     */\r\n    function changeSharedStakeMultiplier(uint256 _sharedStakeMultiplier) external onlyGovernor {\r\n        sharedStakeMultiplier = _sharedStakeMultiplier;\r\n    }\r\n\r\n    /**\r\n     * @dev Change the proportion of arbitration fees that must be paid as fee stake by the winner of the previous round.\r\n     * @param _winnerStakeMultiplier Multiplier of arbitration fees that must be paid as fee stake. In basis points.\r\n     */\r\n    function changeWinnerStakeMultiplier(uint256 _winnerStakeMultiplier) external onlyGovernor {\r\n        winnerStakeMultiplier = _winnerStakeMultiplier;\r\n    }\r\n\r\n    /**\r\n     * @dev Change the proportion of arbitration fees that must be paid as fee stake by the party that lost the previous round.\r\n     * @param _loserStakeMultiplier Multiplier of arbitration fees that must be paid as fee stake. In basis points.\r\n     */\r\n    function changeLoserStakeMultiplier(uint256 _loserStakeMultiplier) external onlyGovernor {\r\n        loserStakeMultiplier = _loserStakeMultiplier;\r\n    }\r\n\r\n    /**\r\n     * @dev Change the address of connectedTCR, the Generalized TCR instance that stores addresses of TCRs related to this one.\r\n     * @param _connectedTCR The address of the connectedTCR contract to use.\r\n     */\r\n    function changeConnectedTCR(address _connectedTCR) external onlyGovernor {\r\n        emit ConnectedTCRSet(_connectedTCR);\r\n    }\r\n\r\n    /**\r\n     * @dev Change the address of the relay contract.\r\n     * @param _relayerContract The new address of the relay contract.\r\n     */\r\n    function changeRelayerContract(address _relayerContract) external onlyGovernor {\r\n        relayerContract = _relayerContract;\r\n    }\r\n\r\n    /**\r\n     * @notice Changes the params related to arbitration.\r\n     * @dev Effectively makes all new items use the new set of params.\r\n     * @param _arbitrator Arbitrator to resolve potential disputes. The arbitrator is trusted to support appeal periods and not reenter.\r\n     * @param _arbitratorExtraData Extra data for the trusted arbitrator contract.\r\n     * @param _registrationMetaEvidence The URI of the meta evidence object for registration requests.\r\n     * @param _clearingMetaEvidence The URI of the meta evidence object for clearing requests.\r\n     */\r\n    function changeArbitrationParams(\r\n        IArbitrator _arbitrator,\r\n        bytes calldata _arbitratorExtraData,\r\n        string calldata _registrationMetaEvidence,\r\n        string calldata _clearingMetaEvidence\r\n    ) external onlyGovernor {\r\n        _doChangeArbitrationParams(_arbitrator, _arbitratorExtraData, _registrationMetaEvidence, _clearingMetaEvidence);\r\n    }\r\n\r\n    /* Internal */\r\n\r\n    /**\r\n     * @dev Effectively makes all new items use the new set of params.\r\n     * @param _arbitrator Arbitrator to resolve potential disputes. The arbitrator is trusted to support appeal periods and not reenter.\r\n     * @param _arbitratorExtraData Extra data for the trusted arbitrator contract.\r\n     * @param _registrationMetaEvidence The URI of the meta evidence object for registration requests.\r\n     * @param _clearingMetaEvidence The URI of the meta evidence object for clearing requests.\r\n     */\r\n    function _doChangeArbitrationParams(\r\n        IArbitrator _arbitrator,\r\n        bytes memory _arbitratorExtraData,\r\n        string memory _registrationMetaEvidence,\r\n        string memory _clearingMetaEvidence\r\n    ) internal {\r\n        emit MetaEvidence(2 * arbitrationParamsChanges.length, _registrationMetaEvidence);\r\n        emit MetaEvidence(2 * arbitrationParamsChanges.length + 1, _clearingMetaEvidence);\r\n\r\n        arbitrationParamsChanges.push(\r\n            ArbitrationParams({arbitrator: _arbitrator, arbitratorExtraData: _arbitratorExtraData})\r\n        );\r\n    }\r\n\r\n    /**\r\n     * @notice Make a fee contribution.\r\n     * @dev It cannot be inlined in fundAppeal because of the stack limit.\r\n     * @param _itemID The item receiving the contribution.\r\n     * @param _requestID The request to contribute.\r\n     * @param _roundID The round to contribute.\r\n     * @param _side The side for which to contribute.\r\n     * @param _contributor The contributor.\r\n     * @param _amount The amount contributed.\r\n     * @param _totalRequired The total amount required for this side.\r\n     * @return The amount of appeal fees contributed.\r\n     */\r\n    function contribute(\r\n        bytes32 _itemID,\r\n        uint256 _requestID,\r\n        uint256 _roundID,\r\n        uint256 _side,\r\n        address payable _contributor,\r\n        uint256 _amount,\r\n        uint256 _totalRequired\r\n    ) internal {\r\n        Round storage round = requestsDisputeData[_itemID][_requestID].rounds[_roundID];\r\n        uint256 pendingAmount = _totalRequired.subCap(round.amountPaid[_side]);\r\n\r\n        // Take up to the amount necessary to fund the current round at the current costs.\r\n        uint256 contribution; // Amount contributed.\r\n        uint256 remainingETH; // Remaining ETH to send back.\r\n        if (pendingAmount > _amount) {\r\n            contribution = _amount;\r\n        } else {\r\n            contribution = pendingAmount;\r\n            remainingETH = _amount - pendingAmount;\r\n        }\r\n\r\n        round.contributions[_contributor][_side] += contribution;\r\n        round.amountPaid[_side] += contribution;\r\n        round.feeRewards += contribution;\r\n\r\n        // Reimburse leftover ETH.\r\n        if (remainingETH > 0) {\r\n            // Deliberate use of send in order to not block the contract in case of reverting fallback.\r\n            _contributor.send(remainingETH);\r\n        }\r\n\r\n        if (contribution > 0) {\r\n            emit Contribution(_itemID, _requestID, _roundID, msg.sender, contribution, Party(_side));\r\n        }\r\n    }\r\n\r\n    // ************************ //\r\n    // *       Getters        * //\r\n    // ************************ //\r\n\r\n    /**\r\n     * @dev Gets the evidengeGroupID for a given item and request.\r\n     * @param _itemID The ID of the item.\r\n     * @param _requestID The ID of the request.\r\n     * @return The evidenceGroupID\r\n     */\r\n    function getEvidenceGroupID(bytes32 _itemID, uint256 _requestID) public pure returns (uint256) {\r\n        return uint256(keccak256(abi.encodePacked(_itemID, _requestID)));\r\n    }\r\n\r\n    /**\r\n     * @notice Gets the arbitrator for new requests.\r\n     * @dev Gets the latest value in arbitrationParamsChanges.\r\n     * @return The arbitrator address.\r\n     */\r\n    function arbitrator() external view returns (IArbitrator) {\r\n        return arbitrationParamsChanges[arbitrationParamsChanges.length - 1].arbitrator;\r\n    }\r\n\r\n    /**\r\n     * @notice Gets the arbitratorExtraData for new requests.\r\n     * @dev Gets the latest value in arbitrationParamsChanges.\r\n     * @return The arbitrator extra data.\r\n     */\r\n    function arbitratorExtraData() external view returns (bytes memory) {\r\n        return arbitrationParamsChanges[arbitrationParamsChanges.length - 1].arbitratorExtraData;\r\n    }\r\n\r\n    /**\r\n     * @dev Gets the number of times MetaEvidence was updated.\r\n     * @return The number of times MetaEvidence was updated.\r\n     */\r\n    function metaEvidenceUpdates() external view returns (uint256) {\r\n        return arbitrationParamsChanges.length;\r\n    }\r\n\r\n    /**\r\n     * @dev Gets the contributions made by a party for a given round of a request.\r\n     * @param _itemID The ID of the item.\r\n     * @param _requestID The request to query.\r\n     * @param _roundID The round to query.\r\n     * @param _contributor The address of the contributor.\r\n     * @return contributions The contributions.\r\n     */\r\n    function getContributions(\r\n        bytes32 _itemID,\r\n        uint256 _requestID,\r\n        uint256 _roundID,\r\n        address _contributor\r\n    ) external view returns (uint256[3] memory contributions) {\r\n        DisputeData storage disputeData = requestsDisputeData[_itemID][_requestID];\r\n        Round storage round = disputeData.rounds[_roundID];\r\n        contributions = round.contributions[_contributor];\r\n    }\r\n\r\n    /**\r\n     * @dev Returns item's information. Includes the total number of requests for the item\r\n     * @param _itemID The ID of the queried item.\r\n     * @return status The current status of the item.\r\n     * @return numberOfRequests Total number of requests for the item.\r\n     * @return sumDeposit The total deposit made by the requester and the challenger (if any)\r\n     */\r\n    function getItemInfo(bytes32 _itemID)\r\n        external\r\n        view\r\n        returns (\r\n            Status status,\r\n            uint256 numberOfRequests,\r\n            uint256 sumDeposit\r\n        )\r\n    {\r\n        Item storage item = items[_itemID];\r\n        return (item.status, item.requestCount, item.sumDeposit);\r\n    }\r\n\r\n    /**\r\n     * @dev Gets information on a request made for the item.\r\n     * @param _itemID The ID of the queried item.\r\n     * @param _requestID The request to be queried.\r\n     * @return disputed True if a dispute was raised.\r\n     * @return disputeID ID of the dispute, if any.\r\n     * @return submissionTime Time when the request was made.\r\n     * @return resolved True if the request was executed and/or any raised disputes were resolved.\r\n     * @return parties Address of requester and challenger, if any.\r\n     * @return numberOfRounds Number of rounds of dispute.\r\n     * @return ruling The final ruling given, if any.\r\n     * @return arbitrator The arbitrator trusted to solve disputes for this request.\r\n     * @return arbitratorExtraData The extra data for the trusted arbitrator of this request.\r\n     * @return metaEvidenceID The meta evidence to be used in a dispute for this case.\r\n     */\r\n    function getRequestInfo(bytes32 _itemID, uint256 _requestID)\r\n        external\r\n        view\r\n        returns (\r\n            bool disputed,\r\n            uint256 disputeID,\r\n            uint256 submissionTime,\r\n            bool resolved,\r\n            address payable[3] memory parties,\r\n            uint256 numberOfRounds,\r\n            Party ruling,\r\n            IArbitrator requestArbitrator,\r\n            bytes memory requestArbitratorExtraData,\r\n            uint256 metaEvidenceID\r\n        )\r\n    {\r\n        Item storage item = items[_itemID];\r\n        require(item.requestCount > _requestID, \"Request does not exist.\");\r\n\r\n        Request storage request = items[_itemID].requests[_requestID];\r\n\r\n        submissionTime = request.submissionTime;\r\n        parties[uint256(Party.Requester)] = request.requester;\r\n        parties[uint256(Party.Challenger)] = request.challenger;\r\n\r\n        (disputed, disputeID, numberOfRounds, ruling) = getRequestDisputeData(_itemID, _requestID);\r\n\r\n        (requestArbitrator, requestArbitratorExtraData, metaEvidenceID) = getRequestArbitrationParams(\r\n            _itemID,\r\n            _requestID\r\n        );\r\n        resolved = getRequestResolvedStatus(_itemID, _requestID);\r\n    }\r\n\r\n    /**\r\n     * @dev Gets the dispute data relative to a given item request.\r\n     * @param _itemID The ID of the queried item.\r\n     * @param _requestID The request to be queried.\r\n     * @return disputed True if a dispute was raised.\r\n     * @return disputeID ID of the dispute, if any.\r\n     * @return ruling The final ruling given, if any.\r\n     * @return numberOfRounds Number of rounds of dispute.\r\n     */\r\n    function getRequestDisputeData(bytes32 _itemID, uint256 _requestID)\r\n        internal\r\n        view\r\n        returns (\r\n            bool disputed,\r\n            uint256 disputeID,\r\n            uint256 numberOfRounds,\r\n            Party ruling\r\n        )\r\n    {\r\n        DisputeData storage disputeData = requestsDisputeData[_itemID][_requestID];\r\n\r\n        return (\r\n            disputeData.status >= DisputeStatus.AwaitingRuling,\r\n            disputeData.disputeID,\r\n            disputeData.roundCount,\r\n            disputeData.ruling\r\n        );\r\n    }\r\n\r\n    /**\r\n     * @dev Gets the arbitration params relative to a given item request.\r\n     * @param _itemID The ID of the queried item.\r\n     * @param _requestID The request to be queried.\r\n     * @return arbitrator The arbitrator trusted to solve disputes for this request.\r\n     * @return arbitratorExtraData The extra data for the trusted arbitrator of this request.\r\n     * @return metaEvidenceID The meta evidence to be used in a dispute for this case.\r\n     */\r\n    function getRequestArbitrationParams(bytes32 _itemID, uint256 _requestID)\r\n        internal\r\n        view\r\n        returns (\r\n            IArbitrator arbitrator,\r\n            bytes memory arbitratorExtraData,\r\n            uint256 metaEvidenceID\r\n        )\r\n    {\r\n        Request storage request = items[_itemID].requests[_requestID];\r\n        ArbitrationParams storage arbitrationParams = arbitrationParamsChanges[request.arbitrationParamsIndex];\r\n\r\n        return (\r\n            arbitrationParams.arbitrator,\r\n            arbitrationParams.arbitratorExtraData,\r\n            2 * request.arbitrationParamsIndex + uint256(request.requestType)\r\n        );\r\n    }\r\n\r\n    /**\r\n     * @dev Gets the resovled status of a given item request.\r\n     * @param _itemID The ID of the queried item.\r\n     * @param _requestID The request to be queried.\r\n     * @return resolved True if the request was executed and/or any raised disputes were resolved.\r\n     */\r\n    function getRequestResolvedStatus(bytes32 _itemID, uint256 _requestID) internal view returns (bool resolved) {\r\n        Item storage item = items[_itemID];\r\n\r\n        if (item.requestCount == 0) {\r\n            return false;\r\n        }\r\n\r\n        if (_requestID < item.requestCount - 1) {\r\n            // It was resolved because it is not the last request.\r\n            return true;\r\n        }\r\n\r\n        return item.sumDeposit == 0;\r\n    }\r\n\r\n    /**\r\n     * @dev Gets the information of a round of a request.\r\n     * @param _itemID The ID of the queried item.\r\n     * @param _requestID The request to be queried.\r\n     * @param _roundID The round to be queried.\r\n     * @return appealed Whether appealed or not.\r\n     * @return amountPaid Tracks the sum paid for each Party in this round.\r\n     * @return hasPaid True if the Party has fully paid its fee in this round.\r\n     * @return feeRewards Sum of reimbursable fees and stake rewards available to the parties that made contributions to the side that ultimately wins a dispute.\r\n     */\r\n    function getRoundInfo(\r\n        bytes32 _itemID,\r\n        uint256 _requestID,\r\n        uint256 _roundID\r\n    )\r\n        external\r\n        view\r\n        returns (\r\n            bool appealed,\r\n            uint256[3] memory amountPaid,\r\n            bool[3] memory hasPaid,\r\n            uint256 feeRewards\r\n        )\r\n    {\r\n        Item storage item = items[_itemID];\r\n        require(item.requestCount > _requestID, \"Request does not exist.\");\r\n\r\n        DisputeData storage disputeData = requestsDisputeData[_itemID][_requestID];\r\n        require(disputeData.roundCount > _roundID, \"Round does not exist\");\r\n\r\n        Round storage round = disputeData.rounds[_roundID];\r\n        appealed = _roundID < disputeData.roundCount - 1;\r\n\r\n        hasPaid[uint256(Party.Requester)] = appealed || round.sideFunded == Party.Requester;\r\n        hasPaid[uint256(Party.Challenger)] = appealed || round.sideFunded == Party.Challenger;\r\n\r\n        return (appealed, round.amountPaid, hasPaid, round.feeRewards);\r\n    }\r\n}\r\n\r\n\r\n\r\n/**\r\n *  @title LightGTCRFactory\r\n *  This contract acts as a registry for LightGeneralizedTCR instances.\r\n */\r\ncontract LightGTCRFactory {\r\n    /**\r\n     *  @dev Emitted when a new Generalized TCR contract is deployed using this factory.\r\n     *  @param _address The address of the newly deployed Generalized TCR.\r\n     */\r\n    event NewGTCR(LightGeneralizedTCR indexed _address);\r\n\r\n    LightGeneralizedTCR[] public instances;\r\n    address public GTCR;\r\n\r\n    /**\r\n     *  @dev Constructor.\r\n     *  @param _GTCR Address of the generalized TCR contract that is going to be used for each new deployment.\r\n     */\r\n    constructor(address _GTCR) public {\r\n        GTCR = _GTCR;\r\n    }\r\n\r\n    /**\r\n     *  @dev Deploy the arbitrable curated registry.\r\n     *  @param _arbitrator Arbitrator to resolve potential disputes. The arbitrator is trusted to support appeal periods and not reenter.\r\n     *  @param _arbitratorExtraData Extra data for the trusted arbitrator contract.\r\n     *  @param _connectedTCR The address of the TCR that stores related TCR addresses. This parameter can be left empty.\r\n     *  @param _registrationMetaEvidence The URI of the meta evidence object for registration requests.\r\n     *  @param _clearingMetaEvidence The URI of the meta evidence object for clearing requests.\r\n     *  @param _governor The trusted governor of this contract.\r\n     *  @param _baseDeposits The base deposits for requests/challenges as follows:\r\n     *  - The base deposit to submit an item.\r\n     *  - The base deposit to remove an item.\r\n     *  - The base deposit to challenge a submission.\r\n     *  - The base deposit to challenge a removal request.\r\n     *  @param _challengePeriodDuration The time in seconds parties have to challenge a request.\r\n     *  @param _stakeMultipliers Multipliers of the arbitration cost in basis points (see LightGeneralizedTCR MULTIPLIER_DIVISOR) as follows:\r\n     *  - The multiplier applied to each party's fee stake for a round when there is no winner/loser in the previous round.\r\n     *  - The multiplier applied to the winner's fee stake for an appeal round.\r\n     *  - The multiplier applied to the loser's fee stake for an appeal round.\r\n     *  @param _relayContract The address of the relay contract to add/remove items directly.\r\n     */\r\n    function deploy(\r\n        IArbitrator _arbitrator,\r\n        bytes memory _arbitratorExtraData,\r\n        address _connectedTCR,\r\n        string memory _registrationMetaEvidence,\r\n        string memory _clearingMetaEvidence,\r\n        address _governor,\r\n        uint256[4] memory _baseDeposits,\r\n        uint256 _challengePeriodDuration,\r\n        uint256[3] memory _stakeMultipliers,\r\n        address _relayContract\r\n    ) public {\r\n        LightGeneralizedTCR instance = clone(GTCR);\r\n        instance.initialize(\r\n            _arbitrator,\r\n            _arbitratorExtraData,\r\n            _connectedTCR,\r\n            _registrationMetaEvidence,\r\n            _clearingMetaEvidence,\r\n            _governor,\r\n            _baseDeposits,\r\n            _challengePeriodDuration,\r\n            _stakeMultipliers,\r\n            _relayContract\r\n        );\r\n        instances.push(instance);\r\n        emit NewGTCR(instance);\r\n    }\r\n\r\n    /**\r\n     * @notice Adaptation of @openzeppelin/contracts/proxy/Clones.sol.\r\n     * @dev Deploys and returns the address of a clone that mimics the behaviour of `GTCR`.\r\n     * @param _implementation Address of the contract to clone.\r\n     * This function uses the create opcode, which should never revert.\r\n     */\r\n    function clone(address _implementation) internal returns (LightGeneralizedTCR instance) {\r\n        assembly {\r\n            let ptr := mload(0x40)\r\n            mstore(ptr, 0x3d602d80600a3d3981f3363d3d373d3d3d363d73000000000000000000000000)\r\n            mstore(add(ptr, 0x14), shl(0x60, _implementation))\r\n            mstore(add(ptr, 0x28), 0x5af43d82803e903d91602b57fd5bf30000000000000000000000000000000000)\r\n            instance := create(0, ptr, 0x37)\r\n        }\r\n        require(instance != LightGeneralizedTCR(0), \"ERC1167: create failed\");\r\n    }\r\n\r\n    /**\r\n     * @return The number of deployed tcrs using this factory.\r\n     */\r\n    function count() external view returns (uint256) {\r\n        return instances.length;\r\n    }\r\n}"
    }
  }
}